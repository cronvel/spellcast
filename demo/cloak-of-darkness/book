
[[doctype spellcast/book]]

[[extension rpg-scripts]]

[module] @@{core}/utils.kfg
[module] @@{rpg}/system/inventory.kfg



[item-model cloak-of-darkness]
	class: object
	name: Cloak of Darkness
	description: A black velvet cloak.
	params:
		examine-flavor: The cloak seems to be light-absorbent!
	slot-type: cloak
	slot-count: 1

[on rpg:drop-item]
	[global]
	[default]
	[if $args.model = "cloak-of-darkness"]
		[message]
			> Hey! You can't drop such a classy cloak on the ground!
			>
		[failure]



[item-model sword-of-slashing]
	class: object
	name: Sword of Slashing
	description: A mighty sword.
	params:
		examine-flavor: Oh my gawd! That mighty sword get a +999 bonus at slashing goblins!
	slot-type: hand
	slot-count: 1



[item-model coin]
	class: object
	name: coin
	description: A coin.
	params:
		examine-flavor: A silver coin.



# Create the main character
[create-main-entity]
	class: character
	name: the hero
	slots:
		cloak: 1
		hand: 2
	equipped-items:
		cloak:
			- cloak-of-darkness
		hand:
			- sword-of-slashing



[here-actions]
	[call rpg:inventory/here-grab]
	[call rpg:inventory/here-inventory]



[panel]
	-	label: $> Inventory
		image: /icons/bag.svg
		id: inventory



[chapter game]
	
	[starting-scene intro1]
		
		[define $damages] 0
		[message]
			>> It's night, and you have been chased relentlessly by goblin hunters,
			>> through the street of this once great town.
			>>
			>> Now you have found refuge into an abandonned opera house.
		
		[next intro2]
	
	
	
	[scene intro2]
		[message] You just secured the entrance of the opera house.
		[next foyer]
	
	
	
	[scene foyer]
		
		[create-item $local.coin] coin
		
		[here persistent]
			items:
				- $local.coin
		
		[message]
			> The hall is spacious. Someone lighted few chandeliers.
			> There are doorways south and west.
		
		[next taverne] $= ( $taverne is-set? ) ? "Go to the taverne (south)" "Go south"
		[next cloakroom] $= ( $cloakroom is-set? ) ? "Go to the cloakroom (west)" "Go west"
		[next street]
			[label] > Go back to the street
			[on-trigger]
				[call core:utils/confirm] > Are you sure? This could be dangerous with all those goblins hunting you!

	
	
	[scene cloakroom]
		[here persistent]
			items: <Array>
		
		[define $cloakroom] true
		
		[debug] $player
		
		[on rpg:drop-item]
			[if $args.model = "cloak-of-darkness"]
				[important-message]
					> You put the Cloak of Darkness on the brass hook.
					>
		
		[message] > This is the cloakroom.
		
		[if $here.items has-item-matching "model" "cloak-of-darkness"]
			[message] > There is a fine black velvet cloak hanged on a brass hook.
		[else]
			[message] > There is a small brass hook fixed on the wall.
		
		[next foyer] Go back to the foyer (east)

	
	
	[scene taverne]
		[define $taverne] true
		
		#[if $player has-equipped-item-matching "model" "cloak-of-darkness"]
		[debug] $player
		
		[if $player has-item-matching "model" "cloak-of-darkness"]
			[message]
				> The taverne is very dark. You can't even see your feet!
				> There is a dim light coming from the foyer.
			
			[next foyer] Go back to the foyer (north, light)
			
			[fake-next once]
				[label] Go east (dark)
				[on-trigger]
					[message] Ouch! You bump against a wall.
					[inc $damages]
			[fake-next once]
				[label] Go west (dark)
				[on-trigger]
					[message] Ouch! You hurt yourself against a furniture.
					[inc $damages]
			[fake-next once]
				[label] Go south (dark)
				[on-trigger]
					[message] Ouch! You hurt yourself against a table.
					[inc $damages]
		[else]
			[message]
				> The taverne is a bit dark, but you can see faint magical light dancing around you.
				> There is a scroll on the floor.
			
			[next foyer] Go back to the foyer (north, light)
			[next scroll] Read the scroll

	
	
	[scene scroll]
		[if $damages < 2]
			[message]
				> The scroll is full of luminous magical symbols: ^M^+this is a scroll of teleportation!^:
				> As your surrounding fades away, you can see in the middle of the scroll, written in fine calligraphy:
				>
				> “You win!”
			[win]
		[else]
			[message]
				> The scroll is blood-spattered.
				> It's a magical scroll but its magic has been dramatically altered by the blood.
				> It glows red for a moment, and a word appears in the middle:
				>
				> “You lose...”
			[lost]
	

	
	[scene street]
		[message] > A dozen of goblin hunters notice you, then suddenly rush you with a grin.
		
		[if $player has-equipped-item-matching "model" "sword-of-slashing"]
			[message]
				> The battle was epic, and your mighty Sword of Slashing helped you to carry the day.
				> You only got few scratches and you've definitively slash'em all...
				> ... but you know that more hunters will come on you in a minute.
			[add $damages] 1
		[elseif $player has-item-matching "model" "sword-of-slashing"]
			[message]
				> You were not ready to fight!
				> You quickly reach the hilt of your Sword of Slashing, but a spear pierces one of your arm.
				> The wound is not deep enough to stop you, but it will penalize you in the fight.
				> The battle was fierce and intense, but your Sword of Slashing helped you to carry the day.
				> You are exhausted and you know that more hunters will come on you in a minute.
			[add $damages] 2
			[equip $player]
				item: $= get-item-matching $player "model" "sword-of-slashing"
		[else]
			[message]
				> You try to quickly reach the hilt of your sword only to realize that you have abandonned
				> it inside the opera house... ^R^+You are empty handed!^:
				> The goblins rejoyce and charge you with their spears.
				> You heroically defended your life, punching and kicking goblins with all you have.
				> You eventually knock out three of them with few powerful blows...
				> ... but that was not enough.
				> Cut after cut, you are losing your blood and your strength...
				> Exhausted, you kneel down...
				> ... ... ...
			[lost]
		
		[message] > You retreat yourself back in the opera house and secure the door again.
		
		[next foyer]


