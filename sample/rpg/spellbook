
[[doctype adventurer]]

[action drink]
	[message]
		$> You drink a cup of vine...

[entity-model player]
	class: character
	label: the hero
	stats:
		fighting: 16
		strength: 12
		hp: 20
		mana: 20
	status:
		hp: 20
		mana: 20
		xp: 0

[entity-model goblin]
	class: character
	label:
		- the pesky goblin
		- the dirty goblin
		- the mad goblin
		- the maniac goblin
		- the goofy goblin
		- the irritating goblin
		- the irksome goblin
		- the bald-headed goblin
		- the insane goblin
		- the shaggy goblin
		- the hairy goblin
		- the stinking goblin
		- the green goblin
		- the brown goblin
		- the glaucous goblin
		- the tiny goblin
		- the edgy goblin
		- the restless goblin
	stats:
		xpReward: 2
		fighting: 8
		strength: 6
		hp: 6
	status:
		hp: 6

[entity-model guardian]
	class: character
	label: the guardian
	params:
		charge: 0.5
	stats:
		xpReward: 10
		fighting: 14
		strength: 16
		hp: 20
	status:
		hp: 20

[role hero]
	[label] You
	[entity] player



[chapter intro]
	[scene init]
		[next goblin-camp]
			[label] Multiple foes.
		[next first-encounter]
			[label] One foe.
	
	[scene goblin-camp]
		[message]
			$> You are in a middle of a goblin camp, there are three tents.
			$> A goblin go out of his tent, see you, and shouts!
			$> Five other goblins are going out of their tents, ready to fight you!
		
		[subscene battle/init]
			[args]
				foes:
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
		
		[next first-encounter]

	[scene first-encounter]
		[message]
			$> You are walking alongside a dangerous cliff.
			$> Suddenly, a big and tough guy appears.
			$> He does not say much except grumbling something like “I'm the guardian” and start rushing you with a giant club.
		
		[subscene battle/init]
			[args]
				foes:
					- guardian
				deadlyPush:
					bonus: 0
					flavour:
						$$> ^R${args.attacker.label//uc1} pushes ${args.defender.label} and ${args.defender.label} falls off the cliff!
		
		[next second-encounter]

	[scene second-encounter]
		[message]
			$> You are moving slowly on a wooden beam, over a chasm.
			$> You find it really difficult to walk without falling, yet, another guardian appears at the other end of the beam.
			$> Instead of waiting for you, he starts walking on the beam toward you.
			$> He swings the air with its giant club as he taunts you, inviting you to go ahead fighting him.
		
		[subscene battle/init]
			[args]
				foes:
					- guardian
				deadlyPush:
					bonus: 6
					flavour:
						$$> ^RAs ${args.attacker.label} charges, ${args.defender.label} slips from the wooden beam!
						$$> ${args.defender.label//uc1} falls into the chasm!



[chapter battle]
	
	[scene init]
		#[message] $> Hello ${roles.hero.entity.label}
		[set $this.hero] $roles.hero.entity
		[set $this.foes] <Array>
		[set $this.turnIndex] 0
		[set $this.turns]
			- $this.hero
		
		[foreach $args.foes => $index : $type]
			[create-entity $this.foes[$index]] $args.foes[$index]
			[set $this.foes[$index].status.cooldown] 0
			[set $this.foes[$index].side] foes
			[append $this.turns] $this.foes[$index]
		
		#[debug info] $this.foes
		#[debug info] $this.turns
		
		[set $this.hero.status.cooldown] 0
		[set $this.hero.side] allies
		
		[set $this.deadlyPush] $args.deadlyPush
		
		[next initiative]
	
	
	
	[scene initiative]
		[foreach $this.turns => $index : $entity]
			[set $this.turns[$index].status.initiativeScore] $= $this.turns[$index].stats.fighting + ( 2 D 8 )
		
		[sort $this.turns]
			key: $status.initiativeScore
			order: desc
		
		#[debug info] $this.turns
		[message] $> ${this.turns[0].label//uc1} takes the initiative.
		[goto new-turn]
		
	
	
	[scene new-turn]
		[set $this.entityTurn] $= $this.turns[$this.turnIndex]
		
		[if $this.entityTurn = $this.hero]
			[goto hero-turn]
		[else]
			[goto foe-turn]
	
	
	
	[scene end-turn]
		[set $this.turnIndex] $= ( $this.turnIndex + 1 ) % $this.turns.length
		
		[if $this.hero.status.hp <= 0]
			[goto lost]
		
		[set $continue] false
		
		[foreach $this.foes => $index : $entity]
			#[debug info] $entity
			[if $entity.status.hp > 0]
				# At least one enemy is alive, start a new turn
				[goto new-turn]
		
		[goto win]
	
	
	
	[scene foe-turn]
		[if $this.entityTurn.status.hp <= 0]
			[goto end-turn]
		
		[if $this.entityTurn.status.cooldown > 0]
			[set $this.entityTurn.status.cooldown] $= $this.entityTurn.status.cooldown - 1
			[goto end-turn]
		
		[if $this.entityTurn.params.charge && ( ( random ) <= $this.entityTurn.params.charge )]
			[subscene charge]
				[args]
					attacker: $this.entityTurn
					defender: $this.hero
			[goto end-turn]
		
		[subscene melee]
			[args]
				attacker: $this.entityTurn
				defender: $this.hero
		
		[goto end-turn]
		
	
	
	[scene hero-turn]
		[if $this.hero.status.cooldown > 0]
			[set $this.hero.status.cooldown] $= $this.hero.status.cooldown - 1
			[goto end-turn]
		
		[next hero-melee]
			[label] Attack
		
		[next hero-charge]
			[label] Charge!
	
		[next hero-fireball]
			[label] Fireball
	
	
	
	[scene hero-target]
		[set $count] 0
		
		[foreach $this.foes => $index : $entity]
			[if $entity.status.hp > 0]
				[set $count] $= $count + 1
				[set $this.target] $this.foes[$index]
		
		[if $count <= 1]
			[return]
		
		[message] Target?
		
		[foreach $this.foes => $index : $entity]
			[if $entity.status.hp > 0]
				[next]
					[args]
						index: $index
					[label] $> ${entity.label//uc1}
					[on-trigger]
						[set $this.target] $this.foes[$args.index]
	
	
	
	[scene hero-melee]
		[subscene hero-target]
		[subscene melee]
			[args]
				attacker: $this.hero
				defender: $this.target
		[goto end-turn]
	
	
	
	[scene hero-charge]
		[subscene hero-target]
		[subscene charge]
			[args]
				attacker: $this.hero
				defender: $this.target
		[goto end-turn]
	
	
	
	[scene hero-fireball]
		[subscene hero-target]
		[subscene fireball]
			[args]
				attacker: $this.hero
				defender: $this.target
		[goto end-turn]
	
	
	
	[scene win]
		[message] $> ${this.hero.label//uc1} win the battle.
		[set $xpReward] 0
		[foreach $this.foes => $index : $entity]
			[set $xpReward] $= $xpReward + $this.foes[$index].stats.xpReward
		[set $this.hero.status.xp] $= $this.hero.status.xp + $xpReward
		[message] $> You gain ${xpReward} xp (=${this.hero.status.xp}).
	
	
	
	[scene lost]
		[message] $> ${this.hero.label//uc1} died on the battlefield.
		[lost]

	
	
	[scene deal-damages]
		[if $args.base]
			[set $this.damages] $args.base
		[else]
			[set $this.damages] $= random ( $args.attacker.stats.strength / 4 ) ( $args.attacker.stats.strength / 2 )
		
		[if $args.mul]
			[set $this.damages] $= $this.damages * $args.mul
		[if $args.add]
			[set $this.damages] $= $this.damages + $args.add
		
		[set $this.damages] $= round $this.damages
		[set $args.defender.status.hp] $= $args.defender.status.hp - $this.damages
		
		[apply-to $flavour] $args.flavour
		[message] $flavour
		
		[if $args.defender.status.hp <= 0]
			[message] $> ^r${args.defender.label//uc1} is dead.
	
	
	
	[scene splash-damages]
		# /!\ Should randomize the array first...
		[foreach $this.turns => $target]
			[if $args.targetCount <= 0]
				[break]
			
			[if ( $target.status.hp <= 0 )]
				[continue]
			[if ( $target = $args.defender ) || ( $target = $args.attacker )]
				[continue]
			[if ( $target.side = $args.attacker.side )]
				[continue]
			
			[if ( random ) <= $args.targetChance )]
				[apply-to $this.damages] $args.damages
				[set $args.targetCount] $= $args.targetCount - 1
				[set $target.status.hp] $= $target.status.hp - $this.damages
				[apply-to $flavour] $args.flavour
				[message] $flavour
				
				[if ( $target.status.hp <= 0 )]
					[message] $> ^r${target.label//uc1} is dead.
			
	
	
	[scene melee]
		[set $this.attackScore] $= $args.attacker.stats.fighting + ( 2 D 8 )
		[set $this.defenseScore] $= $args.defender.stats.fighting + ( 2 D 8 )
		
		[if $this.attackScore > $this.defenseScore]
			#[message] $> ${this.hero.stats.damageBase} + ${this.hero.stats.damageDice} D ${this.hero.stats.damageFaces}
			[subscene deal-damages]
				[args]
					attacker: $args.attacker
					defender: $args.defender
					flavour: $$> ^r${args.attacker.label//uc1} hits ${args.defender.label} for ${this.damages} hp.
			
		[else]
			[fortune]
				- $> ${args.attacker.label//uc1} misses ${args.defender.label}.
				- $> ${args.defender.label//uc1} blocks ${args.attacker.label}.
				- $> ${args.defender.label//uc1} avoids ${args.attacker.label}.
	
	
	
	[scene charge]
		#[set $args.attacker.status.cooldown] 1
		[set $this.attackScore] $= ( $args.attacker.stats.fighting + 2 ) + ( 2 D 8 )
		[set $this.defenseScore] $= $args.defender.stats.fighting + ( 2 D 8 )
		#[message] $> Scores: ${this.attackScore} vs ${this.defenseScore}
		
		[message] $> ${args.attacker.label//uc1} charges ${args.defender.label}!
		
		[if $this.attackScore > $this.defenseScore]
			#[message] $> ${this.hero.stats.damageBase} + ${this.hero.stats.damageDice} D ${this.hero.stats.damageFaces}
			[subscene deal-damages]
				[args]
					attacker: $args.attacker
					defender: $args.defender
					flavour: $$> ^r${args.attacker.label//uc1} hits ${args.defender.label} for ${this.damages} hp.
					mul: 1.5
					add: 1
			
			[if $this.deadlyPush]
				[subscene deadly-push]
					[args] $args
		[elseif ( $this.attackScore = $this.defenseScore ) && $this.deadlyPush]
			[message] $> ${args.defender.label//uc1} blocks ${args.attacker.label}.
			[subscene deadly-push]
				[args] $args
		[else]
			[fortune]
				- $> ${args.attacker.label//uc1} misses ${args.defender.label}.
				- $> ${args.defender.label//uc1} blocks ${args.attacker.label}.
				- $> ${args.defender.label//uc1} avoids ${args.attacker.label}.
	
	
	
	[scene fireball]
		[set $args.attacker.status.cooldown] 1
		[set $this.attackScore] $= ( 20 ) + ( 2 D 8 )
		[set $this.defenseScore] $= $args.defender.stats.fighting + ( 2 D 8 )
		#[message] $> Scores: ${this.attackScore} vs ${this.defenseScore}
		
		[message] $> ${args.attacker.label//uc1} cast a ^rfireball^ at ${args.defender.label}!
		
		[if $this.attackScore > $this.defenseScore]
			#[message] $> ${this.hero.stats.damageBase} + ${this.hero.stats.damageDice} D ${this.hero.stats.damageFaces}
			[subscene deal-damages]
				[args]
					attacker: $args.attacker
					defender: $args.defender
					flavour: $$> ^rThe fireball hits ${args.defender.label} for ${this.damages} hp.
					base: $= 8 + ( 2 D 8 )
			
			[subscene splash-damages]
				[args]
					attacker: $args.attacker
					defender: $args.defender
					damages: $$= 4 + ( 2 D 4 )
					flavour: $$> ^yThe ^rfireball^y explosion hits ${target.label} for ${this.damages} hp.
					targetChance: 0.5
					targetCount: 3
			
		[else]
			[fortune]
				- $> The ^rfireball^ misses ${args.defender.label}.
				- $> ${args.defender.label//uc1} evades the ^rfireball^:.
				- $> ${args.defender.label//uc1} avoids the ^rfireball^:.
	
	
	
	[scene deadly-push]
		[set $this.attackScore] $= ( ( max $args.attacker.stats.strength ( $args.attacker.stats.fighting - 3 ) ) + $this.deadlyPush.bonus ) + ( 2 D 6 )
		[set $this.defenseScore] $= $args.defender.stats.strength + ( 2 D 6 )
		
		[if $this.attackScore > $this.defenseScore]
			[set $args.defender.status.hp] 0
			[apply-to $flavour] $this.deadlyPush.flavour
			[message] $flavour
		
		
	
	
