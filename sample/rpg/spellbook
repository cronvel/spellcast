
[[doctype adventurer]]

[action drink]
	[message]
		$> You drink a cup of vine...

[entity-model player]
	class: character
	label: the hero
	stats:
		fighting: 16
		strength: 12
		hp: 20
		mana: 20
	status:
		hp: 20
		mana: 20
		xp: 0

[entity-model goblin]
	class: character
	label:
		- the pesky goblin
		- the dirty goblin
		- the mad goblin
		- the maniac goblin
		- the goofy goblin
		- the irritating goblin
		- the irksome goblin
		- the bald-headed goblin
		- the insane goblin
		- the shaggy goblin
		- the hairy goblin
		- the stinking goblin
		- the green goblin
		- the brown goblin
		- the glaucous goblin
		- the tiny goblin
		- the edgy goblin
		- the restless goblin
	stats:
		xpReward: 2
		fighting: 8
		strength: 6
		hp: 6
	status:
		hp: 6

[entity-model guardian]
	class: character
	label: the guardian
	params:
		charge: 0.5
	stats:
		xpReward: 10
		fighting: 14
		strength: 16
		hp: 20
	status:
		hp: 20

[role hero]
	[label] You
	[entity] player



[chapter intro]
	[scene init]
		[next goblin-camp]
			[label] Multiple foes.
		[next first-encounter]
			[label] One foe.
	
	[scene goblin-camp]
		[message]
			$> You are in a middle of a goblin camp, there are three tents.
			$> A goblin go out of his tent, see you, and shouts!
			$> Five other goblins are going out of their tents, ready to fight you!
		
		[gosub battle/init]
			[args]
				foes:
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
		
		[next first-encounter]

	[scene first-encounter]
		[message]
			$> You are walking alongside a dangerous cliff.
			$> Suddenly, a big and tough guy appears.
			$> He does not say much except grumbling something like “I'm the guardian” and start rushing you with a giant club.
		
		[gosub battle/init]
			[args]
				foes:
					- guardian
				deadlyPush:
					bonus: 0
					flavour:
						$$> ^R${attacker.label//uc1} pushes ${defender.label} and ${defender.label} falls off the cliff!
		
		[next second-encounter]

	[scene second-encounter]
		[message]
			$> You are moving slowly on a wooden beam, over a chasm.
			$> You find it really difficult to walk without falling, yet, another guardian appears at the other end of the beam.
			$> Instead of waiting for you, he starts walking on the beam toward you.
			$> He swings the air with its giant club as he taunts you, inviting you to go ahead fighting him.
		
		[gosub battle/init]
			[args]
				foes:
					- guardian
				deadlyPush:
					bonus: 6
					flavour:
						$$> ^RAs ${attacker.label} charges, ${defender.label} slips from the wooden beam!
						$$> ${defender.label//uc1} falls into the chasm!



[chapter battle]
	
	[scene init]
		[set $battle] <Object>
		
		#[message] $> Hello ${roles.hero.entity.label}
		[set $battle.hero] $roles.hero.entity
		[set $battle.foes] <Array>
		[set $battle.turnIndex] 0
		[set $battle.turns]
			- $battle.hero
		
		[foreach $args.foes => $index : $type]
			[create-entity $battle.foes[$index]] $args.foes[$index]
			[set $battle.foes[$index].status.cooldown] 0
			[set $battle.foes[$index].side] foes
			[append $battle.turns] $battle.foes[$index]
		
		#[debug info] $battle.foes
		#[debug info] $battle.turns
		
		[set $battle.hero.status.cooldown] 0
		[set $battle.hero.side] allies
		
		[set $battle.deadlyPush] $args.deadlyPush
		
		[next initiative]
	
	
	
	[scene initiative]
		[foreach $battle.turns => $entity]
			[set $entity.status.initiativeScore] $= $entity.stats.fighting + ( 2 D 8 )
		
		[sort $battle.turns]
			key: $status.initiativeScore
			order: desc
		
		#[debug info] $battle.turns
		[message] $> ${battle.turns[0].label//uc1} takes the initiative.
		[goto new-turn]
		
	
	
	[scene new-turn]
		[set $battle.entityTurn] $= $battle.turns[$battle.turnIndex]
		
		[if $battle.entityTurn = $battle.hero]
			[goto hero-turn]
		[else]
			[goto foe-turn]
	
	
	
	[scene end-turn]
		[set $battle.turnIndex] $= ( $battle.turnIndex + 1 ) % $battle.turns.length
		
		[if $battle.hero.status.hp <= 0]
			[goto lost]
		
		[foreach $battle.foes => $index : $entity]
			#[debug info] $entity
			[if $entity.status.hp > 0]
				# At least one enemy is alive, start a new turn
				[goto new-turn]
		
		[goto win]
	
	
	
	[scene foe-turn]
		[if $battle.entityTurn.status.hp <= 0]
			[goto end-turn]
		
		[if $battle.entityTurn.status.cooldown > 0]
			[set $battle.entityTurn.status.cooldown] $= $battle.entityTurn.status.cooldown - 1
			[goto end-turn]
		
		[if $battle.entityTurn.params.charge && ( ( random ) <= $battle.entityTurn.params.charge )]
			[gosub charge]
				[args]
					attacker: $battle.entityTurn
					defender: $battle.hero
			[goto end-turn]
		
		[gosub melee]
			[args]
				attacker: $battle.entityTurn
				defender: $battle.hero
		
		[goto end-turn]
		
	
	
	[scene hero-turn]
		[if $battle.hero.status.cooldown > 0]
			[set $battle.hero.status.cooldown] $= $battle.hero.status.cooldown - 1
			[goto end-turn]
		
		[next hero-melee]
			[label] Attack
		
		[next hero-charge]
			[label] Charge!
	
		[next hero-fireball]
			[label] Fireball
	
	
	
	[scene hero-target]
		[set $count] 0
		
		[foreach $battle.foes => $index : $entity]
			[if $entity.status.hp > 0]
				[set $count] $= $count + 1
				[set $battle.target] $battle.foes[$index]
		
		[if $count <= 1]
			[return]
		
		[message] Target?
		
		[foreach $battle.foes => $index : $entity]
			[if $entity.status.hp > 0]
				[next]
					[args]
						index: $index
					[label] $> ${entity.label//uc1}
					[on-trigger]
						[set $battle.target] $battle.foes[$args.index]
	
	
	
	[scene hero-melee]
		[gosub hero-target]
		[gosub melee]
			[args]
				attacker: $battle.hero
				defender: $battle.target
		[goto end-turn]
	
	
	
	[scene hero-charge]
		[gosub hero-target]
		[gosub charge]
			[args]
				attacker: $battle.hero
				defender: $battle.target
		[goto end-turn]
	
	
	
	[scene hero-fireball]
		[gosub hero-target]
		[gosub fireball]
			[args]
				attacker: $battle.hero
				defender: $battle.target
		[goto end-turn]
	
	
	
	[scene win]
		[message] $> ${battle.hero.label//uc1} win the battle.
		[set $xpReward] 0
		[foreach $battle.foes => $index : $entity]
			[set $xpReward] $= $xpReward + $battle.foes[$index].stats.xpReward
		[set $battle.hero.status.xp] $= $battle.hero.status.xp + $xpReward
		[message] $> You gain ${xpReward} xp (=${battle.hero.status.xp}).
	
	
	
	[scene lost]
		[message] $> ${battle.hero.label//uc1} died on the battlefield.
		[lost]

	
	
	[scene deal-damages]
		[if $args.base]
			[set $damages] $args.base
		[else]
			[set $damages] $= random ( $args.attacker.stats.strength / 4 ) ( $args.attacker.stats.strength / 2 )
		
		[if $args.mul]
			[set $damages] $= $damages * $args.mul
		[if $args.add]
			[set $damages] $= $damages + $args.add
		
		[set $damages] $= round $damages
		[set $args.defender.status.hp] $= $args.defender.status.hp - $damages
		
		[apply-to $flavour] $args.flavour
		[message] $flavour
		
		[if $args.defender.status.hp <= 0]
			[message] $> ^r${args.defender.label//uc1} is dead.
	
	
	
	[scene splash-damages]
		# /!\ Should randomize the array first...
		[foreach $battle.turns => $target]
			[if $args.targetCount <= 0]
				[break]
			
			[if ( $target.status.hp <= 0 )]
				[continue]
			[if ( $target = $args.defender ) || ( $target = $args.attacker )]
				[continue]
			[if ( $target.side = $args.attacker.side )]
				[continue]
			
			[if ( random ) <= $args.targetChance )]
				[apply-to $damages] $args.damages
				[set $args.targetCount] $= $args.targetCount - 1
				[set $target.status.hp] $= $target.status.hp - $damages
				[apply-to $flavour] $args.flavour
				[message] $flavour
				
				[if ( $target.status.hp <= 0 )]
					[message] $> ^r${target.label//uc1} is dead.
			
	
	
	[scene melee]
		[set $attacker] $args.attacker
		[set $defender] $args.defender
		
		[set $attackScore] $= $args.attacker.stats.fighting + ( 2 D 8 )
		[set $defenseScore] $= $args.defender.stats.fighting + ( 2 D 8 )
		
		[if $attackScore > $defenseScore]
			#[message] $> ${battle.hero.stats.damageBase} + ${battle.hero.stats.damageDice} D ${battle.hero.stats.damageFaces}
			[gosub deal-damages]
				[args]
					attacker: $args.attacker
					defender: $args.defender
					flavour: $$> ^r${args.attacker.label//uc1} hits ${args.defender.label} for ${damages} hp.
			
		[else]
			[fortune]
				- $> ${args.attacker.label//uc1} misses ${args.defender.label}.
				- $> ${args.defender.label//uc1} blocks ${args.attacker.label}.
				- $> ${args.defender.label//uc1} avoids ${args.attacker.label}.
	
	
	
	[scene charge]
		[set $attacker] $args.attacker
		[set $defender] $args.defender
		
		#[set $args.attacker.status.cooldown] 1
		[set $attackScore] $= ( $args.attacker.stats.fighting + 2 ) + ( 2 D 8 )
		[set $defenseScore] $= $args.defender.stats.fighting + ( 2 D 8 )
		#[message] $> Scores: ${attackScore} vs ${defenseScore}
		
		[message] $> ${args.attacker.label//uc1} charges ${args.defender.label}!
		
		[if $attackScore > $defenseScore]
			#[message] $> ${battle.hero.stats.damageBase} + ${battle.hero.stats.damageDice} D ${battle.hero.stats.damageFaces}
			[gosub deal-damages]
				[args]
					attacker: $args.attacker
					defender: $args.defender
					flavour: $$> ^r${args.attacker.label//uc1} hits ${args.defender.label} for ${damages} hp.
					mul: 1.5
					add: 1
			
			[if $battle.deadlyPush]
				[gosub deadly-push]
					[args] $args
		[elseif ( $attackScore = $defenseScore ) && $battle.deadlyPush]
			[message] $> ${args.defender.label//uc1} blocks ${args.attacker.label}.
			[gosub deadly-push]
				[args] $args
		[else]
			[fortune]
				- $> ${args.attacker.label//uc1} misses ${args.defender.label}.
				- $> ${args.defender.label//uc1} blocks ${args.attacker.label}.
				- $> ${args.defender.label//uc1} avoids ${args.attacker.label}.
	
	
	
	[scene fireball]
		[set $attacker] $args.attacker
		[set $defender] $args.defender
		
		[set $attacker.status.mana] $attacker.status.mana - 6
		[message] $> mana: ${attacker.status.mana}
		[if $attacker.status.mana < 0]
			[set $attacker.status.mana] 0
			[message] Not enough mana!
			[return]
		
		[set $args.attacker.status.cooldown] 1
		[set $attackScore] $= ( 20 ) + ( 2 D 8 )
		[set $defenseScore] $= $args.defender.stats.fighting + ( 2 D 8 )
		#[message] $> Scores: ${attackScore} vs ${defenseScore}
		
		[message] $> ${args.attacker.label//uc1} cast a ^rfireball^ at ${args.defender.label}!
		
		[if $attackScore > $defenseScore]
			#[message] $> ${battle.hero.stats.damageBase} + ${battle.hero.stats.damageDice} D ${battle.hero.stats.damageFaces}
			[gosub deal-damages]
				[args]
					attacker: $args.attacker
					defender: $args.defender
					flavour: $$> ^rThe fireball hits ${args.defender.label} for ${damages} hp.
					base: $= 8 + ( 2 D 8 )
			
			[gosub splash-damages]
				[args]
					attacker: $args.attacker
					defender: $args.defender
					damages: $$= 4 + ( 2 D 4 )
					flavour: $$> ^yThe ^rfireball^y explosion hits ${target.label} for ${damages} hp.
					targetChance: 0.5
					targetCount: 3
			
		[else]
			[fortune]
				- $> The ^rfireball^ misses ${args.defender.label}.
				- $> ${args.defender.label//uc1} evades the ^rfireball^:.
				- $> ${args.defender.label//uc1} avoids the ^rfireball^:.
	
	
	
	[scene deadly-push]
		[set $attackScore] $= ( ( max $args.attacker.stats.strength ( $args.attacker.stats.fighting - 3 ) ) + $battle.deadlyPush.bonus ) + ( 2 D 6 )
		[set $defenseScore] $= $args.defender.stats.strength + ( 2 D 6 )
		
		[if $attackScore > $defenseScore]
			[set $args.defender.status.hp] 0
			[apply-to $flavour] $battle.deadlyPush.flavour
			[message] $flavour
		
		
	
	
