
[[doctype adventurer]]

[action drink]
	[message]
		$> You drink a cup of vine...

[entity-model player]
	class: character
	label: the hero
	stats:
		fighting: 16
		strength: 12
		hp: 20
		mana: 20
	status:
		hp: 20
		mana: 20
		xp: 0

[entity-model dog]
	class: character
	label: the dog
	stats:
		xpReward: 5
		fighting: 12
		strength: 12
		hp: 12
	status:
		hp: 12

[entity-model goblin]
	class: character
	label:
		- the pesky goblin
		- the dirty goblin
		- the mad goblin
		- the maniac goblin
		- the goofy goblin
		- the irritating goblin
		- the irksome goblin
		- the bald-headed goblin
		- the insane goblin
		- the angry goblin
		- the shaggy goblin
		- the hairy goblin
		- the stinking goblin
		- the green goblin
		- the brown goblin
		- the glaucous goblin
		- the tiny goblin
		- the edgy goblin
		- the restless goblin
		- the shivering goblin
		- the malicious goblin
		- the nasty goblin
		- the disgusting goblin
		- the ugly goblin
	stats:
		xpReward: 2
		fighting: 8
		strength: 6
		hp: 6
	status:
		hp: 6

[entity-model guardian]
	class: character
	label: the guardian
	params:
		charge: 0.5
	stats:
		xpReward: 10
		fighting: 14
		strength: 16
		hp: 20
	status:
		hp: 20

[role hero]
	[label] You
	[entity] player



[chapter intro]
	[scene init]
		[next goblin-camp]
			[label] Multiple foes.
		[next first-encounter]
			[label] One foe.
	
	[scene goblin-camp]
		[message]
			$> You are in a middle of a goblin camp, there are half a dozen of tents.
			$> A goblin go out of his tent, see you, and shouts!
			$> Twelve other goblins are going out of their tents, ready to fight you!
			$> The stray dog that was following you barks angrily, and jump in the battle at your side!
		
		[create-entity $dog]
			model: dog
			label: the stray dog
		
		[gosub battle/init]
			[args]
				allies:
					- $roles.hero.entity
					- $dog
				foes:
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
					- goblin
		
		[next first-encounter]

	[scene first-encounter]
		[message]
			$> You are walking alongside a dangerous cliff.
			$> Suddenly, a big and tough guy appears.
			$> He does not say much except grumbling something like “I'm the guardian” and start rushing you with a giant club.
		
		[gosub battle/init]
			[args]
				allies:
					- $roles.hero.entity
				foes:
					- guardian
				deadlyPush:
					bonus: 0
					flavour:
						$$> ^R${attacker.label//uc1} pushes ${defender.label} and ${defender.label} falls off the cliff!
		
		[next second-encounter]

	[scene second-encounter]
		[message]
			$> You are moving slowly on a wooden beam, over a chasm.
			$> You find it really difficult to walk without falling, yet, another guardian appears at the other end of the beam.
			$> Instead of waiting for you, he starts walking on the beam toward you.
			$> He swings the air with its giant club as he taunts you, inviting you to go ahead fighting him.
		
		[gosub battle/init]
			[args]
				allies:
					- $roles.hero.entity
				foes:
					- guardian
				deadlyPush:
					bonus: 6
					flavour:
						$$> ^RAs ${attacker.label} charges, ${defender.label} slips from the wooden beam!
						$$> ${defender.label//uc1} falls into the chasm!



[chapter battle]
	
	[scene init]
		[set $battle] <Object>
		
		[set $battle.foes] <Array>
		[set $battle.turnIndex] 0
		[set $battle.turns] <Array>
		
		[foreach $args.foes => $index : $foe]
			[if $foe is-entity?]
				[set $battle.foes[$index]] $foe
			[else]
				[create-entity $battle.foes[$index]] $foe
			[set $battle.foes[$index].side] foes
			[append $battle.turns] $battle.foes[$index]
		
		[foreach $args.allies => $index : $ally]
			[if $ally is-entity?]
				[set $battle.allies[$index]] $ally
			[else]
				[create-entity $battle.allies[$index]] $ally
			[set $battle.allies[$index].side] allies
			[append $battle.turns] $battle.allies[$index]
			
		
		#[debug info] $battle.allies
		#[debug info] $battle.foes
		#[debug info] $battle.turns
		
		# Init all entities
		[foreach $battle.turns => $index : $type]
			[set $battle.turns[$index].status.cooldown] 0
		
		[set $battle.deadlyPush] $args.deadlyPush
		
		[next initiative]
	
	
	
	[scene initiative]
		[foreach $battle.turns => $entity]
			[set $entity.status.initiativeScore] $= $entity.stats.fighting + ( 2 D 8 )
		
		[sort $battle.turns]
			key: $status.initiativeScore
			order: desc
		
		#[debug info] $battle.turns
		[message] $> ${battle.turns[0].label//uc1} takes the initiative.
		[goto new-turn]
		
	
	
	[scene new-turn]
		[set $battle.entityTurn] $= $battle.turns[$battle.turnIndex]
		
		[if $battle.entityTurn.status.hp <= 0]
			[goto end-turn]
		
		[if $battle.entityTurn.status.cooldown > 0]
			[set $battle.entityTurn.status.cooldown] $= $battle.entityTurn.status.cooldown - 1
			[goto end-turn]
		
		[if $battle.entityTurn.npc]
			[goto npc-turn]
		[else]
			[goto pc-turn]
	
	
	
	[scene end-turn]
		[set $battle.turnIndex] $= ( $battle.turnIndex + 1 ) % $battle.turns.length
		
		[set $alliesAlive] 0
		[set $foesAlive] 0
		
		[foreach $battle.allies => $entity]
			[if $entity.status.hp > 0]
				[set $alliesAlive] $= $alliesAlive + 1
		
		[foreach $battle.foes => $entity]
			[if $entity.status.hp > 0]
				[set $foesAlive] $= $foesAlive + 1
		
		#[message] $> Alive: ${alliesAlive} ${foesAlive}
		
		[if ! $alliesAlive]
			[goto lost]
		[elseif ! $foesAlive]
			[goto win]
		[else]
			[goto new-turn]
	
	
	
	[scene npc-turn]
		
		[gosub npc-targeting]
		
		[if $battle.entityTurn.params.charge && ( ( random ) <= $battle.entityTurn.params.charge )]
			[gosub charge]
				[args]
					attacker: $battle.entityTurn
					defender: $battle.target
			[goto end-turn]
		
		[gosub melee]
			[args]
				attacker: $battle.entityTurn
				defender: $battle.target
		
		[goto end-turn]
		
	
	
	[scene pc-turn]
		[next pc-melee]
			[label] Attack
		
		[next pc-charge]
			[label] Charge!
	
		[next pc-fireball]
			[label] Fireball
	
	
	
	[scene npc-targeting]
		
		[if $battle.entityTurn.side = "allies"]
			[set $enemies] $battle.foes
		[else]
			[set $enemies] $battle.allies
		
		[set $enemiesAlive] <Array>
		
		[foreach $enemies => $entity]
			[if $entity.status.hp > 0]
				[append $enemiesAlive] $entity
				[set $battle.target] $entity
		
		[if $enemiesAlive.length <= 1]
			[return]
		
		[set $index] $= random ( $enemiesAlive.length - 1 )
		[set $battle.target] $enemiesAlive[$index]
	
	
	
	[scene pc-targeting]
		[set $count] 0
		
		[if $battle.entityTurn.side = "allies"]
			[set $enemies] $battle.foes
		[else]
			[set $enemies] $battle.allies
		
		[foreach $enemies => $entity]
			[if $entity.status.hp > 0]
				[set $count] $= $count + 1
				[set $battle.target] $entity
		
		[if $count <= 1]
			[return]
		
		[message] Target?
		
		[foreach $enemies => $index : $entity]
			[if $entity.status.hp > 0]
				[next]
					[args]
						index: $index
					[label] $> ${entity.label//uc1}
					[on-trigger]
						[set $battle.target] $enemies[$args.index]
	
	
	
	[scene pc-melee]
		[gosub pc-targeting]
		[gosub melee]
			[args]
				attacker: $battle.entityTurn
				defender: $battle.target
		[goto end-turn]
	
	
	
	[scene pc-charge]
		[gosub pc-targeting]
		[gosub charge]
			[args]
				attacker: $battle.entityTurn
				defender: $battle.target
		[goto end-turn]
	
	
	
	[scene pc-fireball]
		[gosub pc-targeting]
		[gosub fireball]
			[args]
				attacker: $battle.entityTurn
				defender: $battle.target
		[goto end-turn]
	
	
	
	[scene win]
		[message] $> ${battle.allies[0].label//uc1} win the battle.
		[set $xpReward] 0
		
		[foreach $battle.foes => $entity]
			[set $xpReward] $= $xpReward + $entity.stats.xpReward
		
		[set $xpReward] $= ceil ( $xpReward / $battle.allies.length )
		
		[foreach $battle.allies => $index : $entity]
			[set $entity.status.xp] $= $entity.status.xp + $xpReward
		
		[message] $> You gain ${xpReward} xp (=${battle.allies[0].status.xp}).
	
	
	
	[scene lost]
		[message] $> ${battle.allies[0].label//uc1} died on the battlefield.
		[lost]

	
	
	[scene deal-damages]
		[if $args.base]
			[set $damages] $args.base
		[else]
			[set $damages] $= random ( $attacker.stats.strength / 4 ) ( $attacker.stats.strength / 2 )
		
		[if $args.mul]
			[set $damages] $= $damages * $args.mul
		[if $args.add]
			[set $damages] $= $damages + $args.add
		
		[set $damages] $= round $damages
		[set $defender.status.hp] $= $defender.status.hp - $damages
		
		[apply-to $flavour] $args.flavour
		[message] $flavour
		
		[if $defender.status.hp <= 0]
			[message] $> ^r${defender.label//uc1} is dead.
	
	
	
	[scene splash-damages]
		# /!\ Should randomize the array first...
		[foreach $battle.turns => $target]
			[if $args.targetCount <= 0]
				[break]
			
			[if ( $target.status.hp <= 0 )]
				[continue]
			[if ( $target = $defender ) || ( $target = $attacker )]
				[continue]
			[if ( $target.side = $attacker.side )]
				[continue]
			
			[if ( random ) <= $args.targetChance )]
				[apply-to $damages] $args.damages
				[set $args.targetCount] $= $args.targetCount - 1
				[set $target.status.hp] $= $target.status.hp - $damages
				[apply-to $flavour] $args.flavour
				[message] $flavour
				
				[if ( $target.status.hp <= 0 )]
					[message] $> ^r${target.label//uc1} is dead.
			
	
	
	[scene melee]
		[set $attacker] $args.attacker
		[set $defender] $args.defender
		
		[set $attackScore] $= $attacker.stats.fighting + ( 2 D 8 )
		[set $defenseScore] $= $defender.stats.fighting + ( 2 D 8 )
		
		[if $attackScore > $defenseScore]
			[gosub deal-damages]
				[args]
					flavour: $$> ^r${attacker.label//uc1} hits ${defender.label} for ${damages} hp.
			
		[else]
			[fortune]
				- $> ${attacker.label//uc1} misses ${defender.label}.
				- $> ${defender.label//uc1} blocks ${attacker.label}.
				- $> ${defender.label//uc1} avoids ${attacker.label}.
	
	
	
	[scene charge]
		[set $attacker] $args.attacker
		[set $defender] $args.defender
		
		#[set $attacker.status.cooldown] 1
		[set $attackScore] $= ( $attacker.stats.fighting + 2 ) + ( 2 D 8 )
		[set $defenseScore] $= $defender.stats.fighting + ( 2 D 8 )
		#[message] $> Scores: ${attackScore} vs ${defenseScore}
		
		[message] $> ${attacker.label//uc1} charges ${defender.label}!
		
		[if $attackScore > $defenseScore]
			[gosub deal-damages]
				[args]
					flavour: $$> ^r${attacker.label//uc1} hits ${defender.label} for ${damages} hp.
					mul: 1.5
					add: 1
			
			[if $battle.deadlyPush]
				[gosub deadly-push]
		[elseif ( $attackScore = $defenseScore ) && $battle.deadlyPush]
			[message] $> ${defender.label//uc1} blocks ${attacker.label}.
			[gosub deadly-push]
		[else]
			[fortune]
				- $> ${attacker.label//uc1} misses ${defender.label}.
				- $> ${defender.label//uc1} blocks ${attacker.label}.
				- $> ${defender.label//uc1} avoids ${attacker.label}.
	
	
	
	[scene fireball]
		[set $attacker] $args.attacker
		[set $defender] $args.defender
		
		[set $attacker.status.mana] $= $attacker.status.mana - 6
		[message] $> mana: ${attacker.status.mana}
		[if $attacker.status.mana < 0]
			[set $attacker.status.mana] 0
			[message] Not enough mana!
			[return]
		
		[set $attacker.status.cooldown] 1
		[set $attackScore] $= ( 20 ) + ( 2 D 8 )
		[set $defenseScore] $= $defender.stats.fighting + ( 2 D 8 )
		#[message] $> Scores: ${attackScore} vs ${defenseScore}
		
		[message] $> ${attacker.label//uc1} cast a ^rfireball^ at ${defender.label}!
		
		[if $attackScore > $defenseScore]
			[gosub deal-damages]
				[args]
					flavour: $$> ^rThe fireball hits ${defender.label} for ${damages} hp.
					base: $= 8 + ( 2 D 8 )
			
			[gosub splash-damages]
				[args]
					damages: $$= 4 + ( 2 D 4 )
					flavour: $$> ^yThe ^rfireball^y explosion hits ${target.label} for ${damages} hp.
					targetChance: 0.5
					targetCount: 3
			
		[else]
			[fortune]
				- $> The ^rfireball^ misses ${defender.label}.
				- $> ${defender.label//uc1} evades the ^rfireball^:.
				- $> ${defender.label//uc1} avoids the ^rfireball^:.
	
	
	
	[scene deadly-push]
		[set $attackScore] $= ( ( max $attacker.stats.strength ( $attacker.stats.fighting - 3 ) ) + $battle.deadlyPush.bonus ) + ( 2 D 6 )
		[set $defenseScore] $= $defender.stats.strength + ( 2 D 6 )
		
		[if $attackScore > $defenseScore]
			[set $defender.status.hp] 0
			[apply-to $flavour] $battle.deadlyPush.flavour
			[message] $flavour
		
		
	
	
