
[[doctype adventurer]]

[module] @@{core}/rpg/system/inventory.kfg



[item-model cloak-of-darkness]
	class: object
	label: Cloak of Darkness
	description: A black velvet cloak.
	params:
		examineFlavor: The cloak seems to be light-absorbent!
	slotType: cloak
	slotCount: 1

[item-model sword-of-slashing]
	class: object
	label: Sword of Slashing
	description: A mighty sword.
	params:
		examineFlavor: Oh my gawd! That mighty sword get a +999 bonus at slashing goblins!
	slotType: hand
	slotCount: 1

[item-model coin]
	class: object
	label: coin
	description: A coin.
	params:
		examineFlavor: A silver coin.

# Create the main character
[create-main-entity]
	class: character
	label: the hero
	slots:
		cloak: 1
		hand: 2
	equippedItems:
		cloak:
			- cloak-of-darkness
		hand:
			- sword-of-slashing



[here-actions]
	[call inventory/here-grab]
	[call inventory/here-inventory]



[chapter game]
	
	[starting-scene intro1]
		
		[message]
			>> It's night, and you have been chased relentlessly by goblin hunters,
			>> through the street of this abandonned once great town.
			>>
			>> Now you have found refuge into an abandonned opera house.
		
		[next intro2]
	
	
	
	[scene intro2]
		[message] You just secured the entrance of the opera house.
		[next foyer]
	
	
	
	[scene foyer]
		
		[create-item $local.coin] coin
		
		[here persistent]
			items:
				- $local.coin
		
		[message]
			> The hall is spacious. Someone lighted few chandeliers.
			> There are doorways south and west.
		
		[on core/drop-item]
			[if $args.data.model = "cloak-of-darkness"]
				[message]
					> Hey! Don't drop such a classy cloak on the ground!
					>
		
		[next bar] $= ( $bar is-set? ) ? "Go to the bar (south)" "Go south"
		[next cloakroom] $= ( $cloakroom is-set? ) ? "Go to the cloakroom (west)" "Go west"
		[next street] > Return to the street
			#[confirm] > Are you sure? This could be dangerous with all those goblins!

	
	
	[scene cloakroom]
		[here persistent]
			items: <Array>
		
		[define $cloakroom] true
		
		[on core/drop-item]
			[if $args.data.model = "cloak-of-darkness"]
				[message]
					> You put the Cloak of Darkness on the brass hook.
					>
		
		[message] > This is the cloakroom.
		
		[if $here.items has-item-matching "model" "cloak-of-darkness"]
			[message] > There is a fine black velvet cloak hanged on a brass hook.
		[else]
			[message] > There is a small brass hook fixed on the wall.
		
		[next foyer] Go back to the foyer (east)

	
	
	[scene bar]
		[define $bar] true
		[define $damage] 0
		
		#[if $player has-equipped-item-matching "model" "cloak-of-darkness"]
		[if $player has-item-matching "model" "cloak-of-darkness"]
			[message]
				> The bar is very dark. You can't even see your feet!
				> There is a dim light coming from the foyer.
			
			[next foyer] Go back to the foyer (north, light)
			
			[fake-next once]
				[label] Go east (dark)
				[on-trigger]
					[message] Ouch! You bump against a wall.
					[inc $damage]
			[fake-next once]
				[label] Go west (dark)
				[on-trigger]
					[message] Ouch! You hurt yourself against a furniture.
					[inc $damage]
			[fake-next once]
				[label] Go south (dark)
				[on-trigger]
					[message] Ouch! You hurt yourself against a table.
					[inc $damage]
		[else]
			[message]
				> The bar is a bit dark, but you can see faint magical light dancing around you.
				> There is a scroll on the floor.
			
			[next foyer] Go back to the foyer (north, light)
			[next scroll] Read the scroll

	
	
	[scene scroll]
		[if $damage < 2]
			[message] > The message reads: “You win!”
			[win]
		[else]
			[message] > The message reads: “You lose...”
			[lose]
	

	
	[scene street]
		[message] > A dozen of goblin hunters notice you, then suddenly rush you with a grin.
		
		[if $player has-equipped-item-matching "model" "sword-of-slashing"]
			[message]
				> The battle was epic, and your mighty Sword of Slashing helped you to carry the day.
				> You definitively slash'em all...
				> ... but you know that more hunters will come on you in a minute.
		[elseif $player has-item-matching "model" "sword-of-slashing"]
			[message]
				> You were not ready to fight!
				> You quickly reach the hilt of your Sword of Slashing, but a spear pierces one of your arm.
				> The wound is not deep enough to stop you, but it will penalize you in the fight.
				> The battle was fierce and intense, but your Sword of Slashing helped you to carry the day.
				> You are exhausted and you know that more hunters will come on you in a minute.
			[add $damages] 2
			[equip $player]
				item: $= get-item-matching $player "model" "sword-of-slashing"
		[else]
			[message]
				> You try to quickly reach the hilt of your sword only to realize that you have abandonned
				> it inside the opera house... ^+You are empty handed!^:
				> The goblins rejoyce and charge you with their spears.
				> You heroically defended your life, punching and kicking goblins with all you have.
				> You eventually knock out three of them with few powerful blows...
				> ... but that was not enough.
				> Cut after cut, you are losing your blood and your strength...
				> Exhausted, you kneel down...
				> ... ... ...
			[lost]
		
		[message] > You retreat yourself back in the opera house and secure the door again.
		
		[next foyer]


