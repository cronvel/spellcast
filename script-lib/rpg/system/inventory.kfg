
[[doctype spellcast/book]]

[system rpg:inventory]
	
	# Full inventory menu
	[fn here-inventory]
		[fake-next]
			[label] $> Inventory
			[button] inventory
			[on-trigger]
				[reset-here-actions]
				[gosub rpg:inventory/inventory-menu]
					[alt]
					[args]
						performer: $here.performer
						stack: $here.items


	
	[fn here-grab]
		[if $here.items is-empty?]
			[return]
		
		[map $here.items => $local.items] $= $this.name
		
		[if $local.items.length > 4]
			[set $local.items.length] 4
			[append $local.items] $> more
		
		[fake-next]
			[label] $> Grab items (${local.items}[enum:|$|, $| and $])
			[on-trigger]
				[reset-here-actions]
				[gosub rpg:inventory/grab-menu]
					[alt]
					[args]
						performer: $here.performer
						stack: $here.items
	
	
	
	[scene inventory-menu]
		[init-sub]
			[set $performer] $args.performer
			[set $stack] $args.stack
		
		[filter $performer.equipped-item-list => $local.equipped-items] $= $this.class = "object"
		[filter $performer.items => $local.unequipped-items] $= $this.class = "object"
		
		[if ( ! $local.equipped-items.length ) && ( ! $local.unequipped-items.length )]
			[message] $> Empty inventory
			[return]
		
		[message] $> Inventory
		
		[next]
			[label] $> (Done)
			[button] close-alt
		
		[foreach $local.equipped-items => $local.index : $local.item]
			[next inventory-object-menu]
				[args]
					index: $local.index
				[label] $> ${local.item.name} (${local.item.slot-type})
				[on-trigger]
					[set $item] $local.equipped-items[$args.index]
					[set $equipped] true
		
		[foreach $local.unequipped-items => $local.index : $local.item]
			[next inventory-object-menu]
				[args]
					index: $local.index
				[label] $> ${local.item.name}
				[on-trigger]
					[set $item] $local.unequipped-items[$args.index]
					[set $equipped] false
	
	
	
	# Called by inventory-menu
	[scene inventory-object-menu]
		
		[next inventory-menu]
			[label] $> (Done)
			[button] close-alt
		
		
		[fake-next]
			[label] $> Examine
			[on-trigger]
				[if $item.params.examine-flavor]
					[message] $item.params.examine-flavor
				[else]
					[message] $> There is nothing special.
		
		[if $equipped]
			[message]
				$> ^_${item.name//uc1}^ (equipped: ${item.slot-type})
			[next inventory-object-menu]
				[label] $> Unequip ${item.name}
				[on-trigger]
					[set $equipped] false
					[unequip $performer]
						item: $item
			[if $stack]
				[next inventory-menu]
					[label] $> Unequip and drop ${item.name}
					[on-trigger]
						[emit rpg:drop-item] $item
						[on-success]
							[set $equipped] false
							[unequip $performer]
								item: $item
							[drop $performer]
								item: $item
								into: $stack
		[else]
			[message]
				$> ^_${item.name//uc1}^ (inventory)
			[next inventory-object-menu]
				[label] $> Equip ${item.name}
				[on-trigger]
					[set $equipped] true
					[equip $performer]
						item: $item
			[if $stack]
				[next inventory-menu]
					[label] $> Drop ${item.name}
					[on-trigger]
						[emit rpg:drop-item] $item
						[on-success]
							[drop $performer]
								item: $item
								into: $stack
		
		[message]
			$>
			$> ${item.description//uc1/dash2space}
		
	
	
	[scene grab-menu]
		[init-sub]
			[set $performer] $args.performer
			[set $stack] $args.stack
			[set $first] true
		
		#[filter $args.stack => $local.items] $= $this.class = "object"
		
		[if ( $stack.length = 0 ) || $only-one]
			# Immediately exit
			[return]
		
		[if ( $stack.length = 1 ) && $first]
			# Immediately go to grab-object-menu
			[set $first] false
			[set $only-one] true
			[set $item] $stack[0]
			[goto grab-object-menu]
		
		[set $first] false
		
		[message] $> Grab what?
		
		[next]
			[label] $> (Done)
			[button] close-alt
		
		
		[foreach $stack => $local.index : $local.item]
			[next grab-object-menu]
				[args]
					index: $local.index
				[label] $> ${local.item.name}
				[on-trigger]
					[set $item] $stack[$args.index]
		
	
	
	# Called by grab-menu
	[scene grab-object-menu]
		
		[next grab-menu]
			[label] $> (Done)
			[button] close-alt
		
		
		[message]
			$> ^_${item.name//uc1}^:
			$>
			$> ${item.description//uc1/dash2space}
		
		[fake-next once]
			[label] $> Examine
			[on-trigger]
				[if $item.params.examine-flavor]
					[message] $item.params.examine-flavor
				[else]
					[message] $> There is nothing special.
		
		[next grab-menu]
			[label] $> Grab ${item.name}
			[on-trigger]
				[grab $performer]
					item: $item
					from: $stack
		
	
	
	[scene ability-menu]
		[set $performer] $args.performer
		
		[message] $> Which usage?
		
		[next]
			[label] $> (Abort)
			[button] close-alt
		
		[foreach $performer.usage-types => $local.usage]
			[next]
				[args]
					usage: $local.usage
				[label] $> ${local.usage//uc1/dash2space}
				[on-trigger]
					[gosub rpg:inventory/ability-usage-menu]
						performer: $performer
						usage: $args.usage

	
	
	# Equip an ability as primary, or make any equipped item (ability or not) a primary
	[scene ability-usage-menu]
		[set $performer] $args.performer
		[set $usage] $= $args.usage is-set? $args.usage true
		
		[message] $> Use what?
		
		[if $performer.primary[$usage]]
			[set $local.currentPrimary] $performer.primary[$usage]
			[set $local.abort-label] $> Keep using: ${local.currentPrimary.name} (${local.currentPrimary.class})
		[else]
			[set $local.abort-label] $> (Abort)
		
		[filter $performer.equipped-item-list => $local.items]
			$= ( $this.usages[$usage] is-set? ) && ( $this != $performer.primary[$usage] )
		
		[filter $performer.items => $local.moreItems]
			$= ( $this.class = "ability" ) && ( $this.usages[$usage] is-set? )
		
		[concat $local.items] $local.moreItems
		
		#[debug info] $performer.equipped-item-list
		#[debug info] $performer.items
		#[debug info] $local.items
		
		[next]
			[label] $local.abort-label
			[button] close-alt
		
		[foreach $local.items => $local.index : $local.item]
			[next]
				[args]
					index: $local.index
				[label] $> ${local.item.name//uc1} (${local.item.class})
				[on-trigger]
					[set $local.item] $local.items[$args.index]
					[if $local.item.class = "ability"]
						[unequip $performer]
							slot: ability
					[equip $performer]
						item: $local.item
						primary: $usage
	
	
	
	[scene equip-object-menu]
		[set $performer] $args.performer
		[set $local.scheduler] $args.scheduler
		
		[message] $> Which slot?
		
		[next]
			[label] $> (Abort)
			[button] close-alt
		
		[foreach $performer.slots => $local.slot : $local.count]
			[next]
				[args]
					slot: $local.slot
				[label] $> ${local.slot//uc1/dash2space}
				[on-trigger]
					[gosub rpg:inventory/equip-object-slot-menu]
						performer: $performer
						slot: $args.slot
						scheduler: $local.scheduler


	
	# Equip an object
	# Support a scheduler argument
	[scene equip-object-slot-menu]
		[set $performer] $args.performer
		[set $local.scheduler] $args.scheduler
		[set $slot] $= $args.slot is-set? $args.slot "hand"
		
		[if $slot = "hand"]
			[set $equip-verb] $> wield
			[set $unequip-verb] $> unwield
		[else]
			[set $equip-verb] $> equip
			[set $unequip-verb] $> unequip
		
		[message] $> ${equip-verb//uc1} / ${unequip-verb} what?
		
		[set $local.equipped-items] $performer.equipped-items[$slot]
		[filter $performer.items => $local.unequipped-items] $= ( $this.class = "object" ) && ( $this.slot-type = $slot )
		
		#[debug info] $performer.equipped-item-list
		#[debug info] $performer.items
		#[debug info] $local.equipped-items
		
		[next]
			[label] $> (Abort)
			[button] close-alt
		
		[foreach $local.equipped-items => $local.index : $local.item]
			[next]
				[args]
					index: $local.index
				[label] $> ${unequip-verb//uc1} ${local.item.name} (${local.item.class})
				[on-trigger]
					[set $local.item] $local.equipped-items[$args.index]
					
					[if $local.scheduler]
						[schedule-action $local.scheduler]
							performer: $performer
							action: unequip
							action-data:
								performer: $performer
								item: $local.item
							prepare-time: 5
							release-time: 5
							recover-time: 5
					[else]
						[call unequip-object]
							performer: $performer
							item: $local.item
					
					[return] true
		
		[foreach $local.unequipped-items => $local.index : $local.item]
			[next]
				[args]
					index: $local.index
				[label] $> ${equip-verb//uc1} ${local.item.name} (${local.item.class})
				[on-trigger]
					[set $local.item] $local.unequipped-items[$args.index]
					
					[if ! ( $performer can-equip-item $local.item )]
						[message] $> ^KShould ${unequip-verb} an item first!
						[cancel]
					
					[if $local.scheduler]
						[schedule-action $local.scheduler]
							performer: $performer
							action: equip
							action-data:
								performer: $performer
								item: $local.item
							prepare-time: 5
							release-time: 5
							recover-time: 5
					[else]
						[call equip-object]
							performer: $performer
							item: $local.item
					
					[return] true
	
	
	
	[fn equip-object]
		[set $performer] $args.performer
		[set $item] $args.item
		
		[if $item.params.equip-flavor]
			[set $local.template] $= random-element $item.params.equip-flavor
			[apply $local.template => $local.flavor] $
			[message] $local.flavor
		[elseif $item.slot-type = "hand"]
			[message] $> ${performer.name//uc1} wields ${item.name//en:the}.
		[else]
			[message] $> ${performer.name//uc1} equips ${item.name//en:the}.
		
		[equip $performer]
			item: $item
	
	
	
	[fn unequip-object]
		[set $performer] $args.performer
		[set $item] $args.item
		
		[if $item.params.unequip-flavor]
			[set $local.template] $= random-element $item.params.unequip-flavor
			[apply $local.template => $local.flavor] $
			[message] $local.flavor
		[elseif $item.slot-type = "hand"]
			[message] $> ${performer.name//uc1} unwields ${item.name//en:the}.
		[else]
			[message] $> ${performer.name//uc1} unequips ${item.name//en:the}.
		
		[unequip $performer]
			item: $item
	
	
	
	
	
	# Deprecated (used by the fighting system)
	
	

	[scene drop-grab]
		[while ! $local.done]
			[gosub drop-grab-one => $local.done] $args
	
	
	
	[scene drop-grab-one]
		[next-style] inline
		
		[if ( $args.buy-price is-number? ) || ( $args.sell-price is-number? )]
			[if $args.performer.goods.cash is-empty?]
				[set $args.performer.goods.cash] 0
			[message] $> You have ^Y${args.performer.goods.cash}[$:cash-unit]^:.
		
		[next]
			[label] $> (Done)
			[button] close-alt
			[on-trigger]
				[return] true
		
		[next-group-break]
		[call grab-one] $args
		[next-group-break]
		[call drop-one] $args
		
	
	
	[fn grab-one]
		
		[foreach $args.stack => $local.index : $local.item]
			
			[if $local.item.class != "object"]
				[continue]
			
			[if ( $args.buy-price is-number? ) && ( $local.item.own-stats.price is-number? )]
				[set $local.verb] buy
				[set $local.price] $= ceil( $local.item.own-stats.price * $args.buy-price )
				[set $local.price-string] $> (${local.price}[$:cash-unit])
			[else]
				[set $local.verb] grab
				[set $local.price] 0
				[set $local.price-string] ""
			
			[next]
				[args]
					performer: $args.performer
					stack: $args.stack
					item: $local.item
					price: $local.price
				[label] $> ${local.verb//uc1} ${local.item.name//uc1} ${local.price-string}
				[on-trigger]
					[if $args.performer.goods.cash >= $args.price]
						[sub $args.performer.goods.cash] $args.price
						[grab $args.performer]
							item: $args.item
							from: $args.stack
					[else]
						[message] $> Not enough cash!
		
	
	
	[fn drop-one]
		
		[foreach $args.performer.items => $local.index : $local.item]
			[if $local.item.class != "object"]
				[continue]
			
			[if ( $args.sell-price is-number? ) && ( $local.item.own-stats.price is-number? )]
				[set $local.verb] sell
				[set $local.price] $= floor( $local.item.own-stats.price * $args.sell-price )
				[set $local.price-string] $> (${local.price}[$:cash-unit])
			[else]
				[set $local.verb] drop
				[set $local.price] 0
				[set $local.price-string] ""
			
			[next]
				[args]
					performer: $args.performer
					stack: $args.stack
					item: $local.item
					price: $local.price
				[label] $> ${local.verb//uc1} ${local.item.name//uc1} ${local.price-string}
				[on-trigger]
					[add $args.performer.goods.cash] $args.price
					[drop $args.performer]
						item: $args.item
						into: $args.stack
		

