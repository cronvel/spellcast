
[[doctype spellcast/book]]
[[locales locales/*]]

[system rpg:fighting]

	# We have to gosub to that scene, not call to a fn, because we need a fresh scope
	[scene fight]

		[add-to-panel]
			-	label: $> Primary/ability
				image: /icons/primary.svg
				id: primary
			-	label: $> Wield/unwield
				image: /icons/catch.svg
				id: wield
		
		# It would be nice to have true constants support in KFG
		[set $const]
			# Multiplier
			offensiveness-bonus: 1.15
			offensiveness-malus: 0.8
			
			# Exponential base, should multiply 40/20 times to go down to 0.5
			low-pressure: 0.983
			pressure: 0.966
			
			anti-aiming-base: 5
			anti-aiming-factor: 1
			
			initiative-roll:
				critical-factor: 2
				critical-chance: 0.05
			
			melee-attack-roll:
				over-power: 2.5
			
			ranged-aiming-roll:
				over-power: 1.5
			
			ranged-attack-roll:
				over-power: 3
			
			wrestling-attack-roll:
				over-power: 3
		
		[set $fight] <Object>

		[set $fight.foes] <Array>
		[set $fight.allies] <Array>
		[set $fight.turns] <Array>
		[set $fight.distance] $= ( $args.distance is-set? ) ? ( floor ( abs $args.distance ) 2 ) 0
		
		[create-scheduler $fight.scheduler]

		[foreach $args.foes => $local.index : $local.foe]
			[if ! ( $local.foe is-entity? )]
				[create-entity $local.foe] $local.foe
			[set $fight.foes[$local.index]] $local.foe
			[append $fight.turns] $local.foe
			[set $local.foe.adhoc.fight]
				side: foes
				offensiveness: $= ( $local.foe.stances.offensiveness is-set? ) ? $local.foe.stances.offensiveness 0
				press-target: null
				pressure: 0
				speed: 0
				alertness: $= 100 / $local.foe.actual.stats.fighting
			[add-to-scheduler $fight.scheduler]
				subject: $local.foe
				ready-time: $local.foe.adhoc.fight.alertness

		[foreach $args.allies => $local.index : $local.ally]
			[if ! ( $local.ally is-entity? )]
				[create-entity $local.ally] $local.ally
			[set $fight.allies[$local.index]] $local.ally
			[append $fight.turns] $local.ally
			[set $local.ally.adhoc.fight]
				side: allies
				offensiveness: $= ( $local.ally.stances.offensiveness is-set? ) ? $local.ally.stances.offensiveness 0
				press-target: null
				pressure: 0
				speed: 0
				alertness: $= 100 / $local.ally.actual.stats.fighting
			[add-to-scheduler $fight.scheduler]
				subject: $local.ally
				ready-time: $local.ally.adhoc.fight.alertness

		#[debug info] $fight.allies
		#[debug info] $fight.foes
		#[debug info] $fight.alive-foes
		#[debug info] $fight.turns

		[set $fight.deadly-push] $args.deadly-push

		[message] $> ^yThe distance between opponents is ${fight.distance} ${fight.distance}[n?step|steps].

		[call scheduler-loop]



	[fn scheduler-loop]
		
		[set $fight.time] 0
		[set $fight.round] 0
		
		[on $fight.scheduler elapsed]
			[add $fight.time] $args
			#[message] $> elapsed: ${args} -- time: ${fight.time}
			[call manage-distance] $args
			[call battle-lines]

		
		[on $fight.scheduler ready]
			#[message] $> ready: ${args.subject.label}
			
			[set $fight.entity-turn] $= $args.subject
			[set $local.subject] $= $args.subject
			
			# /!\ should we remove it from the scheduler?
			[if $local.subject.status.health <= 0]
				[return]

			[if $local.subject.npc]
				[call npc-turn] $local.subject
			[else]
				[gosub pc-turn] $local.subject
		
		# Not useful, it kicks in immediately after	ready
		#[on $fight.scheduler started]
		#	[message] $> started: ${args.subject.label} ${args.action}
		
		[on $fight.scheduler prepared]
			#[message] $> prepared: ${args.subject.label} ${args.action}
			[set $local.fn] $= $args.action . "-prepared"
			[call $local.fn] $args.action-data
		
		[on $fight.scheduler released]
			#[message] $> released: ${args.subject.label} ${args.action} ${args.action-data.subject}
			[set $local.fn] $= $args.action . "-released"
			[call $local.fn] $args.action-data
		
		#[on $fight.scheduler recovered]
		#	[message] $> recovered: ${args.subject.label} ${args.action}
		#	[set $local.fn] $= $args.action . "-recovered"
		#	[call $local.fn] $args.action-data
		
		[loop]
			[filter $fight.foes => $fight.alive-foes] $= $this.status.health > 0
			[filter $fight.allies => $fight.alive-allies] $= $this.status.health > 0

			[if ! $fight.alive-allies.length]
				[call lost]
				[return]
			[elseif ! $fight.alive-foes.length]
				[call win]
				[return]
			
			[advance-scheduler $fight.scheduler 10]
			#[pause 1]
			#[message] After pause



	[fn manage-distance]
		# Manage distance
		[if $fight.distance > 0]
			[map $fight.alive-allies => $local.allies-speed] $this.adhoc.fight.speed
			[map $fight.alive-foes => $local.foes-speed] $this.adhoc.fight.speed

			[set $local.time-factor] $= $args / 10
			[set $local.speed] $= ( avg $local.allies-speed ) + ( avg $local.foes-speed )
			[set $local.distance] $= max 0 ( round ( $fight.distance - ( $local.speed * $local.time-factor ) ) )

			[if $local.distance != $fight.distance]
				[set $fight.distance] $local.distance
				[if $fight.distance = 0]
					[message] $> ^yThe two opposing crews join together and clash.
				[else]
					[message] $> ^yThe remaining distance between opponents is ${fight.distance} ${fight.distance}[n?step|steps].



	[fn win]
		[message] $> ${fight.allies[0].label//uc1} win the fight.
		[set $local.xp-reward] 0

		[foreach $fight.foes => $local.entity]
			[add $local.xp-reward] $local.entity.params.xp-reward

		[set $local.xp-reward] $= ceil ( $local.xp-reward / $fight.allies.length )

		[foreach $fight.allies => $local.index : $local.entity]
			[add $local.entity.status.xp] $local.xp-reward

		[message] $> You gain ${local.xp-reward} xp (=${fight.allies[0].status.xp}).

		[gosub loot]
			# Temp:
			subject: $fight.allies[0]
			targets: $fight.foes



	[fn lost]
		[message] $> ${fight.allies[0].label//uc1} died on the battlefield.
		[lost]



	[fn all-pressure]
		[foreach $fight.alive-entities => $local.entity]
			[call pressure] $local.entity

	[fn pressure]
		[reduce $fight.alive-entities , 0 => $args.adhoc.fight.pressure]
			$= $this.previous + ( ( $this.current.adhoc.fight.press-target = $args ) ? $this.current.adhoc.fight.press-value 0 )
		# Pressure has a max level, because when the target is "saturated",
		# extra opponents are not able to reach the target properly:
		# both the target and its opponents get some malus, therefore the pressure is capped to 18 (4 charging opponents)
		[set $args.adhoc.fight.pressure] $= min ( max 0 ( $args.adhoc.fight.pressure - 6 ) ) 18



	[fn battle-lines]

		[filter $fight.alive-foes => $front] $= $this.adhoc.fight.offensiveness > 0
		[filter $fight.alive-foes => $middle] $= $this.adhoc.fight.offensiveness = 0
		[filter $fight.alive-foes => $back] $= $this.adhoc.fight.offensiveness < 0

		[set $offset] $= ceil ( ( 1 - $front.length ) / 2 )
		[foreach $front => $index : $entity]
			[set $entity.adhoc.fight.position]
				x: $= $fight.distance / 2
				y: $= $index + $offset

		[set $offset] $= ceil ( ( 1 - $middle.length ) / 2 )
		[foreach $middle => $index : $entity]
			[set $entity.adhoc.fight.position]
				x: $= ( $fight.distance / 2 ) + 1
				y: $= $index + $offset

		[set $offset] $= ceil ( ( 1 - $back.length ) / 2 )
		[foreach $back => $index : $entity]
			[set $entity.adhoc.fight.position]
				x: $= ( $fight.distance / 2 ) + 2
				y: $= $index + $offset

		[sort $fight.alive-foes]
			$= ( $this.left.adhoc.fight.position.y + ( $this.left.adhoc.fight.position.x / 1000 ) )
			$= - ( $this.right.adhoc.fight.position.y + ( $this.right.adhoc.fight.position.x / 1000 ) )

		# Same for allies

		[filter $fight.alive-allies => $front] $= $this.adhoc.fight.offensiveness > 0
		[filter $fight.alive-allies => $middle] $= $this.adhoc.fight.offensiveness = 0
		[filter $fight.alive-allies => $back] $= $this.adhoc.fight.offensiveness < 0

		[set $offset] $= ceil ( ( 1 - $front.length ) / 2 )
		[foreach $front => $index : $entity]
			[set $entity.adhoc.fight.position]
				x: $= - ( $fight.distance / 2 )
				y: $= $index + $offset

		[set $offset] $= ceil ( ( 1 - $middle.length ) / 2 )
		[foreach $middle => $index : $entity]
			[set $entity.adhoc.fight.position]
				x: $= ( - ( $fight.distance / 2 ) ) - 1
				y: $= $index + $offset

		[set $offset] $= ceil ( ( 1 - $back.length ) / 2 )
		[foreach $back => $index : $entity]
			[set $entity.adhoc.fight.position]
				x: $= ( - ( $fight.distance / 2 ) ) - 2
				y: $= $index + $offset

		[sort $fight.alive-allies]
			$= ( $this.left.adhoc.fight.position.y + ( $this.left.adhoc.fight.position.x / 1000 ) )
			$= - ( $this.right.adhoc.fight.position.y + ( $this.right.adhoc.fight.position.x / 1000 ) )
		[set $fight.alive-entities] $= concat $fight.alive-foes $fight.alive-allies



	[fn npc-turn]

		[call npc-targeting]
		#[call npc-targeting => $local.target]

		[set $subject] $fight.entity-turn

		[if $subject.params.battle-cry && ( ( random ) <= $subject.params.battle-cry )]
			[if $subject.params.battle-cry-flavor]
				[set $local.battle-cry] $= random-element $subject.params.battle-cry-flavor
				[apply $local.battle-cry => $local.flavor] $
				[message] $local.flavor
			[else]
				[message] $> ${subject.label//uc1} shouts!


		[if $subject.params.charge && ( ( random ) <= $subject.params.charge )]
			[call charge]
				subject: $subject
				target: $fight.target
			
			[return]

		[call melee-fighting]
			subject: $subject
			target: $fight.target

		[return]



	[fn npc-targeting]

		[set $local.possible-targets] <Array>

		[if ( $fight.entity-turn.adhoc.fight.side = "allies" ) xor ( $args.target-allies )]
			[concat $local.possible-targets] $fight.alive-foes

		[if ( $fight.entity-turn.adhoc.fight.side = "foes" ) xor ( $args.target-allies )]
			[concat $local.possible-targets] $fight.alive-allies

		[set $local.index] $= random ( $local.possible-targets.length - 1 )
		
		# Which one?
		[set $fight.target] $local.possible-targets[$local.index]
		#[return] $local.possible-targets[$local.index]



	[scene pc-turn]
		[next-style] table
		
		[if $fight.distance = 0]
			[set $local.item] $fight.entity-turn.primary.melee-fighting

			[if $local.item.usages.melee-fighting.primary.params.menu-label]
				[set $local.label] $local.item.usages.melee-fighting.primary.params.menu-label
			[else]
				[set $local.label] $> fight

			[next pc-melee-fighting]
				[label] $> ${local.label//uc1}
				[on-trigger]
					[set $fight.entity-turn.adhoc.fight.offensiveness] 0

			[next pc-melee-fighting]
				[label] $> ${local.label//uc1} boldly
				[on-trigger]
					[set $fight.entity-turn.adhoc.fight.offensiveness] 1

			[next pc-melee-fighting]
				[label] $> ${local.label//uc1} cautiously
				[on-trigger]
					[set $fight.entity-turn.adhoc.fight.offensiveness] -1
		[else]
			[next pc-melee-fighting]
				[label] $> March
				[on-trigger]
					[set $fight.entity-turn.adhoc.fight.offensiveness] 0

			[next pc-melee-fighting]
				[label] $> March boldly
				[on-trigger]
					[set $fight.entity-turn.adhoc.fight.offensiveness] 1

			[next pc-melee-fighting]
				[label] $> March cautiously
				[on-trigger]
					[set $fight.entity-turn.adhoc.fight.offensiveness] -1

		[next-group-break]
		
		[next pc-charge] $> Charge!

		[if $fight.entity-turn.primary.ranged-fighting]
			[set $local.item] $fight.entity-turn.primary.ranged-fighting

			[if $local.item.usages.ranged-fighting.primary.params.menu-label]
				[set $local.label] $local.item.usages.ranged-fighting.primary.params.menu-label
			[else]
				[set $local.label] $> Shoot

			[next pc-ranged-fighting] $> ${local.label//uc1}

		[if $fight.entity-turn.primary.battle-ability]
			[set $local.item] $fight.entity-turn.primary.battle-ability

			[if $local.item.usages.battle-ability.primary.params.menu-label]
				[set $local.label] $local.item.usages.battle-ability.primary.params.menu-label
			[else]
				[set $local.label] $> Use ability

			[next pc-battle-ability] $> ${local.label//uc1}

		[next-group-break]
		
		[next pc-turn]
			[label] $> Primary/ability
			[button] primary
			[on-trigger]
				[gosub rpg:inventory/ability-menu]
					[alt]
					[args]
						subject: $fight.entity-turn

		[next pc-turn]
			[label] $> Wield/unwield
			[button] wield
			[on-trigger]
				[gosub rpg:inventory/equip-object-slot-menu => $local.changed]
					[alt]
					[args]
						subject: $fight.entity-turn
						slot: hand
						scheduler: $fight.scheduler
				[if $local.changed]
					[set $fight.entity-turn.adhoc.fight.speed] 0
					[set $fight.entity-turn.adhoc.fight.offensiveness] -1
					[set $fight.entity-turn.adhoc.fight.press-target] null
					[set $fight.entity-turn.adhoc.fight.press-value] 0
					[return]



	[scene pc-targeting]
		[set $local.count] 0

		[set $local.possible-targets] <Array>

		[if ( $fight.entity-turn.adhoc.fight.side = "allies" ) xor ( $args.target-allies )]
			[concat $local.possible-targets] $fight.alive-foes

		[if ( $fight.entity-turn.adhoc.fight.side = "foes" ) xor ( $args.target-allies )]
			[concat $local.possible-targets] $fight.alive-allies

		[if $local.possible-targets.length <= 1]
			[set $fight.target] $local.possible-targets[0]
			[return]

		[message] $> Target?

		[foreach $local.possible-targets => $local.index : $local.entity]
			[if $local.entity.adhoc.fight.offensiveness > 0]
				[set $local.position] "←"
			[elseif $local.entity.adhoc.fight.offensiveness < 0]
				[set $local.position] "→    "
			[else]
				[set $local.position] "   "

			[next]
				[args]
					index: $local.index
				[label] $> ${local.position} ${local.entity.label//uc1}
				[on-trigger]
					[set $fight.target] $local.possible-targets[$args.index]



	[scene pc-melee-fighting]
		[if $fight.distance = 0]
			[gosub pc-targeting]
				[alt]
		[call melee-fighting]
			subject: $fight.entity-turn
			target: $fight.target
		[return]



	[scene pc-charge]
		[if $fight.distance = 0]
			[gosub pc-targeting]
				[alt]
		[call charge]
			subject: $fight.entity-turn
			target: $fight.target
		[return]



	[scene pc-ranged-fighting]
		[gosub pc-targeting]
			[alt]
		[call ranged-fighting]
			subject: $fight.entity-turn
			target: $fight.target
		[return]



	[scene pc-battle-ability]
		[call battle-ability]
			subject: $fight.entity-turn
		[return]



	[fn melee-fighting]
		[set $subject] $args.subject
		[set $target] $args.target
	
		[set $subject.adhoc.fight.speed] $= $subject.adhoc.fight.offensiveness ??? 2 4 6

		[if $fight.distance > 0]
			[set $subject.adhoc.fight.press-target] null
			[set $subject.adhoc.fight.press-value] 0

			[if $subject.adhoc.fight.offensiveness > 0]
				[message] $> ${subject.label//uc1} runs toward the opposing crew.
			[elseif $subject.adhoc.fight.offensiveness < 0]
				[message] $> ${subject.label//uc1} walks cautiously toward the opposing crew.
			[else]
				[message] $> ${subject.label//uc1} marches toward the opposing crew.

		#[message] $> ready time: ${subject.adhoc.fight.alertness}
		#[message] $> prepare time: ${subject.actual-usages.melee-fighting.compound.prepare-time}
		#[message] $> release time: ${subject.actual-usages.melee-fighting.compound.release-time}
		#[message] $> recover time: ${subject.actual-usages.melee-fighting.compound.recover-time}
		
		[schedule-action $fight.scheduler]
			subject: $subject
			action: melee-fighting
			action-data:
				subject: $subject
				target: $target
			ready-time: $subject.adhoc.fight.alertness
			prepare-time: $= $subject.actual-usages.melee-fighting.compound.prepare-time
			release-time: $= $subject.actual-usages.melee-fighting.compound.release-time
			recover-time: $= $subject.actual-usages.melee-fighting.compound.recover-time
		
	
	
	[fn melee-fighting-prepared]
		[set $subject] $args.subject
		[set $target] $args.target
		[set $item] $subject.primary.melee-fighting
	
		# Still out of reach
		[if $fight.distance > 0]
			[return]
	
	
	[fn melee-fighting-released]
		[set $subject] $args.subject
		[set $target] $args.target
		[set $item] $subject.primary.melee-fighting
		
		# Still out of reach
		[if $fight.distance > 0]
			[return]
		
		[set $subject.adhoc.fight.press-target] $target
		[set $subject.adhoc.fight.press-value] $= $subject.adhoc.fight.offensiveness ??? 3 4 6

		[call pressure] $subject
		[call pressure] $target

		[if $subject.actual-usages.melee-fighting.params.spell-level is-set?]
			[call rpg:spellcasting/cast => $local.casted]
				subject: $subject
				item: $item
				usage: $subject.actual-usages.melee-fighting
				bonus-factor: $= $const.low-pressure ^ $subject.adhoc.fight.pressure
			[if ! $local.casted]
				[return]

		[set $local.attack-score] $= $subject.actual-usages.melee-fighting.compound.attack
		[set $local.defense-score] $= $target.actual-usages.melee-fighting.compound.defense

		#[message] $> Pressure -- ${subject.adhoc.fight.pressure} / ${target.adhoc.fight.pressure}
		#[message] $> A: ${local.attack-score} vs ${local.defense-score}

		[if $subject.adhoc.fight.offensiveness > 0]
			[mul $local.attack-score] $const.offensiveness-bonus
			# Less sensible to the pressure when attacking
			[mul $local.attack-score] $= $const.low-pressure ^ $subject.adhoc.fight.pressure
		[elseif $subject.adhoc.fight.offensiveness < 0]
			[mul $local.attack-score] $const.offensiveness-malus
			[mul $local.attack-score] $= $const.pressure ^ $subject.adhoc.fight.pressure
		[else]
			[mul $local.attack-score] $= $const.pressure ^ $subject.adhoc.fight.pressure

		[if $target.adhoc.fight.offensiveness > 0]
			[mul $local.defense-score] $const.offensiveness-malus
			[mul $local.defense-score] $= $const.pressure ^ $target.adhoc.fight.pressure
		[elseif $target.adhoc.fight.offensiveness < 0]
			[mul $local.defense-score] $const.offensiveness-bonus
			# Less sensible to the pressure when defending
			[mul $local.defense-score] $= $const.low-pressure ^ $target.adhoc.fight.pressure
		[else]
			[mul $local.defense-score] $= $const.pressure ^ $target.adhoc.fight.pressure

		#[message] $> Melee: ${local.attack-score} vs ${local.defense-score}
		
		[if success-roll $local.attack-score $local.defense-score $const.melee-attack-roll]
			[call deal-damages]

			[call effects]
				subject: $subject
				target: $target
				position: $target.adhoc.fight.position
				actual-usage: $subject.actual-usages.ranged-fighting

		[else]
			[fortune]
				- $> ${subject.label//uc1} misses ${target.label}.
				- $> ${target.label//uc1} blocks ${subject.label}.
				- $> ${target.label//uc1} avoids ${subject.label}.



	[fn charge]
		[set $subject] $args.subject
		[set $target] $args.target
	
		[set $subject.adhoc.fight.speed] 10
		[set $subject.adhoc.fight.offensiveness] 1

		[if $fight.distance > 0]
			[set $subject.adhoc.fight.press-target] null
			[set $subject.adhoc.fight.press-value] 0
			[message] $> ${subject.label//uc1} charges toward the opposing crew.
		
		[schedule-action $fight.scheduler]
			subject: $subject
			action: charge
			action-data:
				subject: $subject
				target: $target
			ready-time: $subject.adhoc.fight.alertness
			prepare-time: $= $subject.actual-usages.melee-fighting.compound.prepare-time
			release-time: $= $subject.actual-usages.melee-fighting.compound.release-time
			recover-time: $= $subject.actual-usages.melee-fighting.compound.recover-time
		
	
	
	[fn charge-prepared]
		[set $subject] $args.subject
		[set $target] $args.target

	
	
	[fn charge-released]
		[set $subject] $args.subject
		[set $target] $args.target

		# Still out of reach
		[if $fight.distance > 0]
			[return]

		[set $local.attack-score] $= $subject.actual-usages.melee-fighting.compound.attack
		[set $local.defense-score] $= $target.actual-usages.melee-fighting.compound.defense

		[set $subject.adhoc.fight.press-target] $target
		[set $subject.adhoc.fight.press-value] 6

		[call pressure] $subject
		[call pressure] $target

		#[message] $> Pressure -- ${subject.adhoc.fight.pressure} / ${target.adhoc.fight.pressure}
		#[message] $> A: ${local.attack-score} vs ${local.defense-score}

		# A charge is always offensive!
		[mul $local.attack-score] $const.offensiveness-bonus
		# Charge is less sensible to the pressure
		[mul $local.attack-score] $= $const.low-pressure ^ $subject.adhoc.fight.pressure

		[if $target.adhoc.fight.offensiveness > 0]
			[mul $local.defense-score] $const.offensiveness-malus
			[mul $local.defense-score] $= $const.pressure ^ $target.adhoc.fight.pressure
		[elseif $target.adhoc.fight.offensiveness < 0]
			[mul $local.defense-score] $const.offensiveness-bonus
			# Less sensible to the pressure when defending
			[mul $local.defense-score] $= $const.low-pressure ^ $target.adhoc.fight.pressure
		[else]
			[mul $local.defense-score] $= $const.pressure ^ $target.adhoc.fight.pressure

		#[message] $> Charge: ${local.attack-score} vs ${local.defense-score}

		[message] $> ${subject.label//uc1} charges ${target.label}!

		[if success-roll $local.attack-score $local.defense-score $const.melee-attack-roll]
			[call deal-damages]

			[if $fight.deadly-push]
				[call deadly-push]
		[else]
			[fortune]
				- $> ${subject.label//uc1} misses ${target.label}.
				- $> ${target.label//uc1} blocks ${subject.label}.
				- $> ${target.label//uc1} avoids ${subject.label}.



	[fn ranged-fighting]
		[set $subject] $args.subject
		[set $target] $args.target
		
		[set $subject.adhoc.fight.speed] 0
		[set $subject.adhoc.fight.offensiveness] -1
		
		[schedule-action $fight.scheduler]
			subject: $subject
			action: ranged-fighting
			action-data:
				subject: $subject
				target: $target
			ready-time: $subject.adhoc.fight.alertness
			prepare-time: $= $subject.actual-usages.ranged-fighting.compound.prepare-time
			release-time: $= $subject.actual-usages.ranged-fighting.compound.release-time
			recover-time: $= $subject.actual-usages.ranged-fighting.compound.recover-time
	
	
	
	[fn ranged-fighting-prepared]
		[set $subject] $args.subject
		[set $target] $args.target
		[set $item] $subject.primary.ranged-fighting
	
	
	
	[fn ranged-fighting-released]
		[set $subject] $args.subject
		[set $target] $args.target
		[set $item] $subject.primary.ranged-fighting

		[if $item.usages.ranged-fighting.primary.params.projectile-label]
			[set $projectile.label] $item.usages.ranged-fighting.primary.params.projectile-label
		[else]
			[set $projectile] $item

		#[debug info] $projectile
		#[debug info] $item.usages.ranged-fighting.primary.params.projectile-label

		[set $subject.adhoc.fight.press-target] $target
		[set $subject.adhoc.fight.press-value] 4

		[call pressure] $subject
		[call pressure] $target

		[if $subject.actual-usages.ranged-fighting.params.spell-level is-set?]
			[call rpg:spellcasting/cast => $local.casted]
				subject: $subject
				item: $item
				usage: $subject.actual-usages.ranged-fighting
				bonus-factor: $= $const.low-pressure ^ $subject.adhoc.fight.pressure
			[if ! $local.casted]
				[return]

		# We are in close range, we have to test first with the closeRange stat,
		# against the anti-ranged stat
		[if $fight.distance = 0]
			
			[set $local.attack-score] $= $subject.actual-usages.ranged-fighting.compound.close-ranged
			[set $local.defense-score] $= $target.actual-usages.melee-fighting.compound.anti-ranged

			#[message] $> Close range A: ${local.attack-score} vs ${local.defense-score}
			#[message] $> Pressure: ${subject.adhoc.fight.pressure}

			[mul $local.attack-score] $= $const.pressure ^ $subject.adhoc.fight.pressure

			[if $target.adhoc.fight.offensiveness > 0]
				[mul $local.defense-score] $const.offensiveness-malus
				[mul $local.defense-score] $= $const.pressure ^ $target.adhoc.fight.pressure
			[elseif $target.adhoc.fight.offensiveness < 0]
				[mul $local.defense-score] $const.offensiveness-bonus
				# Less sensible to the pressure when defending
				[mul $local.defense-score] $= $const.low-pressure ^ $target.adhoc.fight.pressure
			[else]
				[mul $local.defense-score] $= $const.pressure ^ $target.adhoc.fight.pressure

			#[message] $> Close range: ${local.attack-score} vs ${local.defense-score}

			[if ! ( success-roll $local.attack-score $local.defense-score $const.melee-attack-roll )]
				[message] $> ${target.label//uc1} prevents ${subject.label} from attacking from afar.
				[return]

		[set $local.aiming-score] $= $subject.actual-usages.ranged-fighting.compound.aiming
		[set $local.anti-aiming-score] $= $const.anti-aiming-base + ( $const.anti-aiming-factor * ( $fight.distance + ( abs $target.adhoc.fight.speed ) ) )
		
		#[message] $> Aiming A: ${local.aiming-score} vs ${local.guard-score}
		#[message] $> Pressure: ${subject.adhoc.fight.pressure}

		[mul $local.aiming-score] $= $const.pressure ^ $subject.adhoc.fight.pressure
		
		[set $local.guard-score] $= $target.actual-usages.ranged-fighting.compound.guard
		
		# We use a correlated-success-rolls to avoid decreasing the chance to hit by performing two uncorrelated rolls
		[set $local.aiming] $= correlated-success-rolls $local.aiming-score ( $local.anti-aiming-score , $local.guard-score ) $ranged-aiming-roll
		#[message] $> Aiming: ${local.aiming-score} vs ${local.anti-aiming-score} / ${local.guard-score} => ${local.aiming[0]} / ${local.aiming[1]}
		
		[if ! $local.aiming[0]]
			[message] $> ${subject.label//uc1} misses ${target.label}.
			[return]

		[if ! $local.aiming[1]]
			[if $target.actual-usages.ranged-fighting.params.guard-flavor is-set?]
				[apply $target.actual-usages.ranged-fighting.params.guard-flavor => $local.flavor] $
				[message] $local.flavor
			[else]
				[message] $> ${subject.label//uc1} misses ${target.label}.
			[return]

		[set $local.attack-score] $= $subject.actual-usages.ranged-fighting.compound.attack
		[set $local.defense-score] $= $target.actual-usages.ranged-fighting.compound.defense

		# /!\ FIXME?
		[add $local.defense-score] $= $fight.distance / 5

		#[message] $> Pressure -- ${subject.adhoc.fight.pressure} / ${target.adhoc.fight.pressure}
		#[message] $> Ranged: ${local.attack-score} vs ${local.defense-score}

		# Attack score of ranged-fighting do not depend much on the character but on the weapon

		[if $target.adhoc.fight.offensiveness > 0]
			[mul $local.defense-score] $const.offensiveness-malus
			[mul $local.defense-score] $= $const.pressure ^ $target.adhoc.fight.pressure
		[elseif $target.adhoc.fight.offensiveness < 0]
			[mul $local.defense-score] $const.offensiveness-bonus
			# Less sensible to the pressure when defending
			[mul $local.defense-score] $= $const.low-pressure ^ $target.adhoc.fight.pressure
		[else]
			[mul $local.defense-score] $= $const.pressure ^ $target.adhoc.fight.pressure

		#[message] $> B: ${local.attack-score} vs ${local.defense-score}

		[if success-roll $local.attack-score $local.defense-score $const.ranged-attack-roll]
			[call deal-damages]
				usage: $subject.actual-usages.ranged-fighting

			[call effects]
				subject: $subject
				target: $target
				position: $target.adhoc.fight.position
				usage: $subject.actual-usages.ranged-fighting

		[else]
			[fortune]
				- $> ${target.label//uc1} blocks ${subject.label}'s ${projectile.label}.
				- $> ${target.label//uc1} avoids ${subject.label}'s ${projectile.label}.



	[fn battle-ability]
		[set $subject] $args.subject
		[set $target] $subject
		[set $local.usage] $subject.actual-usages.battle-ability
		[set $item] $subject.primary.battle-ability

		[if $local.usage.params.targeting]
			[if $subject.npc]
				[call npc-targeting]
					target-allies: $local.usage.params.target-allies
			[else]
				[gosub pc-targeting]
					[alt]
					[args]
						target-allies: $local.usage.params.target-allies
			[set $target] $fight.target

		# Check if the ability has its own speed and offensiveness value
		[if $item.params.speed is-number?]
			[set $subject.adhoc.fight.speed] $= $item.params.speed
		[if $item.params.offensiveness is-number?]
			[set $subject.adhoc.fight.offensiveness] $= $item.params.offensiveness

		[schedule-action $fight.scheduler]
			subject: $subject
			action: battle-ability
			action-data:
				subject: $subject
				target: $target
				position: $local.position
			ready-time: $subject.adhoc.fight.alertness
			prepare-time: $= $subject.actual-usages.battle-ability.compound.prepare-time
			release-time: $= $subject.actual-usages.battle-ability.compound.release-time
			recover-time: $= $subject.actual-usages.battle-ability.compound.recover-time

	
	
	[fn battle-ability-prepared]
		[set $subject] $args.subject
		[set $target] $args.target
		[set $local.usage] $subject.actual-usages.battle-ability
		[set $item] $subject.primary.battle-ability
	
	
	
	[fn battle-ability-released]
		[set $subject] $args.subject
		[set $target] $args.target
		[set $local.usage] $subject.actual-usages.battle-ability
		[set $item] $subject.primary.battle-ability

		# Position MUST be set on release time
		[set $local.position] $target.adhoc.fight.position
		
		[call pressure] $subject

		[if $local.usage.params.spell-level is-set?]
			[call rpg:spellcasting/cast => $local.casted]
				subject: $subject
				item: $item
				usage: $local.usage
				bonus-factor: $= $const.low-pressure ^ $subject.adhoc.fight.pressure
			[if ! $local.casted]
				[return]

		[call effects]
			subject: $subject
			target: $target
			position: $local.position
			usage: $local.usage



	[fn equip-prepared] <TagContainer>
	
	[fn equip-released]
		[call rpg:inventory/equip-object] $args
	
	[fn unequip-prepared] <TagContainer>
	
	[fn unequip-released]
		[call rpg:inventory/unequip-object] $args
	
	
	
	[fn deal-damages]
		[if $args.target]
			[set $target] $args.target

		[if ! $args.usage]
			[set $args.usage] $subject.actual-usages.melee-fighting

		#[message] $> Critical factor: ${args.usage.compound.critical-factor}
		
		# Damage level
		[if $args.damages is-real?]
			[set $local.attack-score] $= $args.damages
		[else]
			[set $local.attack-score] $= $args.usage.compound.damages
			
			[if $args.usage.compound.offensiveness-damage-mod]
				# Attacker offensiveness is used only when not using fixed damage base
				[if $subject.adhoc.fight.offensiveness > 0]
					[mul $local.attack-score] $const.offensiveness-bonus
				[elseif $subject.adhoc.fight.offensiveness < 0]
					[mul $local.attack-score] $const.offensiveness-malus

		[if $args.bonus-factor is-real?]
			[mul $local.attack-score] $args.bonus-factor

		[set $local.defense-score] $= $target.actual.stats.resilience

		[if $args.usage.compound.offensiveness-damage-mod]
			[if $target.adhoc.fight.offensiveness > 0]
				[mul $local.defense-score] $const.offensiveness-malus
			[elseif $target.adhoc.fight.offensiveness < 0]
				[mul $local.defense-score] $const.offensiveness-bonus

		#[message] $> Damages: ${local.attack-score} vs ${local.defense-score}

		[set $damages]
			$= quantity-roll $local.attack-score $local.defense-score (
			$=   base: 15
			$=   over-power: 2
			$=   critical-factor: $args.usage.compound.critical-factor
			$=   critical-chance: 0.05
			$=   lower-divide: false
			$=   round: true
			$= )

		#[message] $> damages: ${damages}

		[sub $target.status.health] $damages

		[call rpg:common/update-malus] $target

		[if $damages >= 100]
			[if $args.fatal-hit-flavor is-set?]
				[apply $args.fatal-hit-flavor => $local.flavor] $
			[elseif $args.critical-hit-flavor is-set?]
				[apply $args.critical-hit-flavor => $local.flavor] $
			[elseif $args.hit-flavor is-set?]
				[apply $args.hit-flavor => $local.flavor] $
			[elseif $args.usage.params.fatal-hit-flavor is-set?]
				[apply $args.usage.params.fatal-hit-flavor => $local.flavor] $
			[elseif $args.usage.params.critical-hit-flavor is-set?]
				[apply $args.usage.params.critical-hit-flavor => $local.flavor] $
			[elseif $args.usage.params.hit-flavor is-set?]
				[apply $args.usage.params.hit-flavor => $local.flavor] $
			[else]
				[set $local.flavor] $> ^r${subject.label//uc1} deadly hits ${target.label} for ${damages} hp.
		[elseif $damages >= 25]
			[if $args.critical-hit-flavor is-set?]
				[apply $args.critical-hit-flavor => $local.flavor] $
			[elseif $args.hit-flavor is-set?]
				[apply $args.hit-flavor => $local.flavor] $
			[elseif $args.usage.params.critical-hit-flavor is-set?]
				[apply $args.usage.params.critical-hit-flavor => $local.flavor] $
			[elseif $args.usage.params.hit-flavor is-set?]
				[apply $args.usage.params.hit-flavor => $local.flavor] $
			[else]
				[set $local.flavor] $> ^r${subject.label//uc1} critically hits ${target.label} for ${damages} hp.
		[else]
			[if $args.hit-flavor is-set?]
				[apply $args.hit-flavor => $local.flavor] $
			[elseif $args.usage.params.hit-flavor is-set?]
				[apply $args.usage.params.hit-flavor => $local.flavor] $
			[else]
				[set $local.flavor] $> ^r${subject.label//uc1} hits ${target.label} for ${damages} hp.

		[message] $local.flavor

		[if $target.status.health <= 0]
			[message] $> ^R^+${target.label//uc1} is dead.



	[fn heal-damages]
		[if $args.target]
			[set $target] $args.target

		[if ! $args.usage]
			[set $args.usage] $subject.actual-usages.battle-ability

		#[message] $> ^Khealing -- checking: ${target.label}

		#[if $target.status.health >= 100]
		#	[return]

		# Healing score
		[if $args.healing is-real?]
			[set $local.healing-score] $= $args.healing
		[else]
			[set $local.healing-score] $= $args.usage.compound.healing

		[set $local.defense-score] $= $target.actual.stats.resilience

		[if $args.bonus-factor is-real?]
			[mul $healing-score] $args.bonus-factor
		
		[set $healing]
			$= quantity-roll $local.healing-score $local.defense-score (
			$=   base: 10
			$=   over-power: 1
			$=   critical-factor: $args.usage.compound.critical-factor
			$=   critical-chance: 0.05
			$=   lower-divide: true
			$=   round: true
			$= )
		
		#[message] $> ^Khealing: ${healing}

		# Cap the healing
		[set $healing] $= min $healing ( 100 - $target.status.health )

		[add $target.status.health] $healing

		[call rpg:common/update-malus] $target

		[if $args.flavor is-set?]
			[apply $args.flavor => $local.flavor] $
		[elseif $args.usage.params.flavor is-set?]
			[apply $args.usage.params.flavor => $local.flavor] $
		[else]
			[set $local.flavor] $> ^r${subject.label//uc1} heals ${target.label} for ${healing} hp.

		[message] $local.flavor



	[fn effects]
		[if $args.usage.params.effects is-empty?]
			[return]

		[foreach $args.usage.params.effects => $local.effect]

			[set $local.exclude] <Array>

			[if $local.effect.filter.exclude-self]
				[append $local.exclude] $args.subject

			[if $local.effect.filter.exclude-target]
				[append $local.exclude] $args.target

			[if $local.effect.filter.type = "radius"]
				[call radius-filter => $local.entities]
					exclude: $local.exclude
					position: $args.position
					radius: $local.effect.filter.radius
			[else]
				[set $local.entities]
					- $args.target

			# If the effect has modifiers, apply them now
			[if $local.effect.mods]
				[merge $args.usage.compound , $local.effect.mods => $local.effect-compound]
			[else]
				[set $local.effect-compound] $args.usage.compound

			[if $local.effect.type = "attack"]
				[call attack-effect]
					entities: $local.entities
					attack: $= $local.effect-compound.attack
					damages: $= $local.effect-compound.damages
					defense-type: $local.effect.defense-type
					miss-flavor: $local.effect.miss-flavor
					hit-flavor: $local.effect.hit-flavor
					critical-hit-flavor: $local.effect.critical-hit-flavor
					fatal-hit-flavor: $local.effect.fatal-hit-flavor
			[elseif $local.effect.type = "healing"]
				[call healing-effect]
					entities: $local.entities
					healing: $= $local.effect-compound.healing
					flavor: $local.effect.flavor



	[fn radius-filter]
		#[debug info] $args

		[set $local.entities] <Array>

		[foreach $fight.alive-entities => $local.target]

			[if $args.exclude has $local.target]
				[continue]

			# Clone it, it can be modified
			[clone $local.target-pos] $local.target.adhoc.fight.position
			#[set $local.target-pos] $local.target.adhoc.fight.position

			# If the target is currently attacking/pressing the current position and is not defensive,
			# act as if its y twice as close as the spash y position, because of encirclement
			[set $local.press-pos] $= $local.target.adhoc.fight.press-target.adhoc.fight.position

			[if $local.press-pos && ( $local.target.adhoc.fight.offensiveness >= 0 ) && ( $local.press-pos.x = $args.position.x ) && ( $local.press-pos.y = $args.position.y )]
				# Divide the delta-y by 2
				[set $local.target-pos.y] $= avg $local.target-pos.y $args.position.y

			[set $local.distance] $= hypot ( $local.target-pos.x - $args.position.x ) ( $local.target-pos.y - $args.position.y )

			[if $local.distance <= $args.radius]
				[append $local.entities] $local.target

		[return] $local.entities



	[fn attack-effect]

		[if $args.usage-type is-empty?]
			[set $args.defense-type] ranged-fighting

		[foreach $args.entities => $target]

			[set $local.attack-score] $= $args.attack
			[set $local.defense-score] $= $target.actual-usages[$args.defense-type].compound.defense

			[call pressure] $target

			[if $target.adhoc.fight.offensiveness > 0]
				[mul $local.defense-score] $const.offensiveness-malus
				[mul $local.defense-score] $= $const.pressure ^ $target.adhoc.fight.pressure
			[elseif $target.adhoc.fight.offensiveness < 0]
				[mul $local.defense-score] $const.offensiveness-bonus
				# Less sensible to the pressure when defending
				[mul $local.defense-score] $= $const.low-pressure ^ $target.adhoc.fight.pressure
			[else]
				[mul $local.defense-score] $= $const.pressure ^ $target.adhoc.fight.pressure
			
			#[message] $> Attack-effect: ${local.attack-score} vs ${local.defense-score}
			
			[if success-roll $local.attack-score $local.defense-score $const.melee-attack-roll]
				[call deal-damages]
					target: $target
					hit-flavor: $args.hit-flavor
					critical-hit-flavor: $args.critical-hit-flavor
					fatal-hit-flavor: $args.fatal-hit-flavor
					damages: $args.damages

			[else]
				[if $args.miss-flavor]
					[apply $args.miss-flavor => $local.flavor] $
					[message] $local.flavor



	[fn healing-effect]

		[foreach $args.entities => $target]

			[call heal-damages]
				target: $target
				flavor: $args.flavor
				healing: $args.healing



	[fn deadly-push]
		[set $local.attack-score]
			$= ( max $subject.actual.stats.strength ( $subject.actual.stats.fighting - 3 ) )
			$= * $fight.deadly-push.bonus-factor
		[set $local.defense-score] $= $target.actual.stats.strength
		
		[mul $local.attack-score] $= $const.low-pressure ^ $subject.adhoc.fight.pressure
		[mul $local.defense-score] $= $const.low-pressure ^ $target.adhoc.fight.pressure

		#[message] $> Attacker ${subject.label} offensiveness: ${subject.adhoc.fight.offensiveness}
		[if $subject.adhoc.fight.offensiveness > 0]
			[mul $local.attack-score] $const.offensiveness-bonus
		[elseif $subject.adhoc.fight.offensiveness < 0]
			[mul $local.attack-score] $const.offensiveness-malus

		#[message] $> Defender ${target.label} offensiveness: ${target.adhoc.fight.offensiveness}
		[if $target.adhoc.fight.offensiveness > 0]
			[mul $local.defense-score] $const.offensiveness-malus
		[elseif $target.adhoc.fight.offensiveness < 0]
			[mul $local.defense-score] $const.offensiveness-bonus

		[if success-roll $local.attack-score $local.defense-score $const.wrestling-attack-roll]
			[set $target.status.health] 0
			[apply $fight.deadly-push.flavor => $local.flavor] $
			[message] $local.flavor



	[scene loot]
		[message] $> Loot?
		[next]
			[label] $> Yes
			[on-trigger]
				[call drop => $local.stack] $args
				[gosub rpg:inventory/drop-grab]
					[alt]
					[args]
						subject: $args.subject
						stack: $local.stack
		[next] $> No



	[fn drop]
		[set $local.stack] <Array>

		[foreach $args.targets => $local.entity]
			[drop $local.entity]
				all: true
				unequip: true
				stack: $local.stack

		[return] $local.stack
